{***************************************************************}
{                          viewpoint.ores                    	}
{                       OMAR Resources (ORES)                   }
{***************************************************************}
{                                                               }
{		Contains some standard camera control definitions.      }
{                                                               }
{***************************************************************}
{                Copyright (c) 2000 Hypercosm, Inc.             }
{***************************************************************}


include "system/position.ores";
include "system/rendering.ores";


subject viewpoint
extends
	position
does
	// constructor
	//
	verb new
		at vector location;
		towards vector direction;
		oriented by vector up_vector = <0 0 1>;
	end;
	
	// attributes setting methods
	//
	verb set_viewpoint
		at vector location;
		towards vector direction;
		oriented by vector up_vector = <0 0 1>;
	end;
	
	// renderable methods
	//
	shape instance;
	
	// printable methods
	//
	verb print;
is
	//
	// constructor
	//
	
	
	verb new
		at vector location;
		towards vector direction;
		oriented by vector up_vector = <0 0 1>;
	is
		// call superclass constructor
		//
		position new at location oriented by 
			new orientation towards direction oriented by up_vector;
	end;	// new
	

	//
	// attributes setting methods
	//	
	
	
	verb set_viewpoint
		at vector location;
		towards vector direction;
		oriented by vector up_vector = <0 0 1>;
	is
		set_location to location;
		set_orientation_vectors towards direction oriented by up_vector;	
	end;	// set_viewpoint
	
		
	//
	// renderable methods
	//
	
	
	shape instance is
		orientation static instance with
			move to location;
		end;
	end;	// instance
	
			
	//	
	// printable methods
	//
	
	
	verb print is
		write "viewpoint at ", get_location, " towards ", get_direction, 
			" oriented by ", get_up_vector, ;
	end;	// print
end;	// viewpoint
