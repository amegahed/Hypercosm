{***************************************************************}
{                      vector_volume.ores                       }
{                     OMAR Resources (ORES)                     }
{***************************************************************}
{                                                               }
{       Contains some standard utilities for representing       }
{                    and manipulating grids.                    }
{                                                               }
{***************************************************************}
{              Copyright (c) 2000 Hypercosm, Inc.               }
{***************************************************************}


include "system/vectors.ores";
include "system/native_trans.ores";
include "system/varrays.ores";


subject vector_volume
extends
	varray3 type
does
	// vector transformation methods
	//
    verb direct
        from axes type axes;
    end;    // direct
    verb transform
        from trans type trans;
    end;    // transform
    
    // printable methods
    //
    verb print;
is
	//
	// vector transformation methods
	//
	
	
    verb direct
        from axes type axes;
    is
        for each vector v in vector_volume do
            v = axes oriented v;
        end;
    end;    // direct


    verb transform
        from trans type trans;
    is
        for each vector v in vector_volume do
            v = trans transformed v;
        end;
    end;    // transform
    
    
	//    
    // printable methods
    //
    
    
    verb print is
    	for integer level = min vector_volume .. max vector_volume do
	    	for integer row = min vector_volume[] .. max vector_volume[] do
	    		for integer column = min vector_volume[,] .. max vector_volume[,] do
	    			write "vector volume[", level, ", ", row, ", ", column, "] = ",
	    				vector_volume[level, row, column], ;
	    		end;
	    	end;
	    end;
    end;	// print
end;	// vector_volume
