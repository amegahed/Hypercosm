{***************************************************************}
{ |\  /|												We Put  }
{ | >< Hypercosm 	      3ds_teapot.ores				3d      }
{ |/  \|												To Work }
{***************************************************************}
{                                                               }
{   	Contains teapot definitions that correspond to 			}
{		entities from Discreet's 3D Studio Max software.		}
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}


include "system/3ds_textureable_object.ores";
include "system/3ds_editable_mesh.ores";
include "system/3ds_modifier_stacks.ores";
include "system/3ds_bicubic_patch.ores";
include "system/3ds_shape.ores";
include "system/3ds_line.ores";
include "system/3ds_spline.ores";
include "system/3ds_lathe.ores";
include "system/3ds_ellipse.ores";
include "system/3ds_loft.ores";


subject translated_3ds_teapot
extends
    translated_3ds_collapsable translated_3ds_texturable_object
does
    // constructor
    //
	verb new with
	
		// geometry parameters
		//
	    scalar radius = 1;
	    
	    // tessellation parameters
	    //
	    integer segments = 4;
	    boolean smooth is true;
	        
	    // part parameters
	    //
	    boolean body is true;
	    boolean handle is true;
	    boolean spout is true;
	    boolean lid is true;
	    
	    // texturing parameters
	    //
	    boolean gen_mapping_coords is false;
	end;

    // attributes setting method
    //
	verb set with
	
		// geometry parameters
		//
	    scalar radius = 1;
	    
	    // tessellation parameters
	    //
	    integer segments = 4;
	    boolean smooth is true;
	        
	    // part parameters
	    //
	    boolean body is true;
	    boolean handle is true;
	    boolean spout is true;
	    boolean lid is true;
	    
	    // texturing parameters
	    //
	    boolean gen_mapping_coords is false;
	end;
	    	
	// collapsable method
	//
	translated_3ds_stackable type question get_collapsed;
	
	// recycling methods
	//
	objective translated_3ds_teapot type question get;
	reference verb recycle;	
has
	// geometry attributes
	//
    scalar radius;
    
    // tessellation attributes
    //
    integer segments;
    boolean smooth;
        
    // part attributes
    //
    boolean body;
    boolean handle;
    boolean spout;
    boolean lid;
    
private	
	// recycling attributes
	//
	translated_3ds_teapot type next is none;
is
    // recycling free list
    //
    translated_3ds_teapot type free_list is none;
    
    
    // 
    // constructor
    //
    
    	
	verb new with
	
		// geometry parameters
		//
	    scalar radius = 1;
	    
	    // tessellation parameters
	    //
	    integer segments = 4;
	    boolean smooth is true;
	        
	    // part parameters
	    //
	    boolean body is true;
	    boolean handle is true;
	    boolean spout is true;
	    boolean lid is true;
	    
	    // texturing parameters
	    //
	    boolean gen_mapping_coords is false;
	is
	    translated_3ds_texturable_object new with
	    
            // set texturing parameters
            //
	        gen_mapping_coords is static gen_mapping_coords;
	    end;

		if verbose_recycling then
			write "new 3ds teapot", ;
		end;
		
		// clamp tessellation to minimum value
		//
		if segments < 1 then
		    segments = 1;
		end;
				    
		with translated_3ds_teapot do
		
			// set geometry attributes
			//
		    its radius = radius;
		    
		    // set tessellation attributes
		    //
		    its segments = segments;
		    its smooth is smooth;
		        
		    // set part attributes
		    //
		    its body is body;
		    its handle is handle;
		    its spout is spout;
		    its lid is lid;
	    end;
	end;	// new	


	//
    // attributes setting method
    //
    
    
	verb set with
	
		// geometry parameters
		//
	    scalar radius = 1;
	    
	    // tessellation parameters
	    //
	    integer segments = 4;
	    boolean smooth is true;
	        
	    // part parameters
	    //
	    boolean body is true;
	    boolean handle is true;
	    boolean spout is true;
	    boolean lid is true;
	    
	    // texturing parameters
	    //
	    boolean gen_mapping_coords is false;
	is
		// clamp tessellation to minimum value
		//
		if segments < 1 then
		    segments = 1;
		end;
		
		with translated_3ds_teapot do
		
			// set geometry attributes
			//
		    its radius = radius;
		    
		    // set tessellation attributes
		    //
		    its segments = segments;
		    its smooth is smooth;
		        
		    // set part attributes
		    //
		    its body is body;
		    its handle is handle;
		    its spout is spout;
		    its lid is lid;
		    
            // set texturing parameters
            //
	        its gen_mapping_coords is gen_mapping_coords;
	    end;	
	end;	// set
	
	
    //
	// collapsable method
	//
	

	verb add_body
		to translated_3ds_editable_mesh type editable_mesh;
	is
		static translated_3ds_modifier_stack type modifier_stack;
		
		vector vertices[] = [<0 0 0> <.75 0 .075> <1 0 .45> <.75 0 1.2> <.7 0 1.2>];
		translated_3ds_spline type spline is translated_3ds_connected_spline through [
			translated_3ds_corner_vertex at vertices[1]
			translated_3ds_smooth_vertex at vertices[2]
			translated_3ds_smooth_vertex at vertices[3] 
			translated_3ds_smooth_vertex at vertices[4] 
			translated_3ds_smooth_vertex at vertices[5]
		];
		translated_3ds_line type path is translated_3ds_line type's get;
		translated_3ds_lathe type lathe is translated_3ds_lathe type's get;
		
		path set to [spline] with
			steps = segments div 2;
		end;
		lathe set with
			degrees = 360;
			segments = static segments * 4;
			cap_start is true;
			cap_end is false;
			gen_mapping_coords is static gen_mapping_coords;
		end;	
	
        modifier_stack push translated_3ds_stackable type path;	
        modifier_stack push translated_3ds_stackable type lathe;
		editable_mesh add modifier_stack get_collapsed;
	end;	// add_body
	
	
	verb add_lid 
		to translated_3ds_editable_mesh type editable_mesh;
	is
		static translated_3ds_modifier_stack type modifier_stack;
		
		vector vertices[] = [<.66 0 1.2> <.1 0 1.35> <0 0 1.575>];
		translated_3ds_spline type spline is translated_3ds_connected_spline through [
			translated_3ds_smooth_vertex at vertices[1]
			translated_3ds_smooth_vertex at vertices[2] 
			translated_3ds_bezier_vertex at vertices[3] 
				from <.45 0 0> to <-.45 0 0>
		];
		translated_3ds_line type path is translated_3ds_line type's get;
		translated_3ds_lathe type lathe is translated_3ds_lathe type's get;
		
		path set to [spline] with
			steps = static segments;
		end;
		lathe set with
			degrees = 360;
			segments = static segments * 4;
			cap_start is true;
			cap_end is false;
			gen_mapping_coords is static gen_mapping_coords;
		end;		
	
        modifier_stack push translated_3ds_stackable type path;	
        modifier_stack push translated_3ds_stackable type lathe;
		editable_mesh add modifier_stack get_collapsed;
	end;	// add_lid
	
	
	verb add_handle
		to translated_3ds_editable_mesh type editable_mesh;
	is
		vector vertices[] = [<-.75 0 1> <-1.4 0 .9> <-.9 0 .4>];
		translated_3ds_spline type spline is translated_3ds_connected_spline through [
			translated_3ds_bezier_vertex at vertices[1]
			    from <.5 0 0> to <-.5 0 0>
			translated_3ds_smooth_vertex at vertices[2] 
			translated_3ds_smooth_vertex at vertices[3]
		];
		translated_3ds_line type line is translated_3ds_line type's get;
		translated_3ds_ellipse type ellipse is translated_3ds_ellipse type's get;
		translated_3ds_loft type loft is translated_3ds_loft type's get;
		
		line set to [spline];
		ellipse set with
	        length = .075;
	        width = .15;
		end;
		loft set with
			path is static line;
			cross_section is ellipse;
			path_steps = static segments div 2;
			shape_steps = static segments div 2;
			gen_mapping_coords is static gen_mapping_coords;
	        cap_start is false;
	        cap_end is false;
		end;
		editable_mesh add loft get_collapsed;		
	end;	// add_handle
	

	verb add_patch
		of bicubic_basis type bicubic_basis;
		to translated_3ds_editable_mesh type editable_mesh;
	is
    	translated_3ds_bicubic_patch type patch is translated_3ds_bicubic_patch type's get;
    	patch set to bicubic_basis with
            length_segments = segments;
        	width_segments = segments; 
        	gen_mapping_coords is static gen_mapping_coords;
        end;
        editable_mesh add translated_3ds_stackable type patch get_collapsed;
	end;	// add_patch
	
	
    verb add_spout
		to translated_3ds_editable_mesh type editable_mesh;
    is
    	add_patch of (new_bicubic_basis of [
            [<0.8500 0.2500 0.4000> <1.1500 0.1500 0.5000> <0.3000 -0.1000 0.1000> <0.2228 -0.0835 0.2228>]
            [<0.8500 0.0000 0.6500> <1.0439 0.0000 0.6061> <0.1939 0.0000 -0.0439> <0.1983 0.0000 0.1265>] 
            [<0.0000 0.0000 0.3536> <-0.1500 0.0000 0.1500> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>] 
            [<0.0000 -0.3536 0.0000> <0.0000 -0.2121 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
        	]) to editable_mesh;
		
		add_patch of (new_bicubic_basis of [
            [<0.8500 0.0000 0.6500> <1.0439 0.0000 0.6061> <0.1939 0.0000 -0.0439> <0.1983 0.0000 0.1265>] 
            [<0.8500 -0.2500 0.4000> <1.1500 -0.1500 0.5000> <0.3000 0.1000 0.1000> <0.2228 0.0835 0.2228>] 
            [<0.0000 -0.3536 0.0000> <0.0000 -0.2121 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>] 
            [<0.0000 0.0000 -0.3536> <0.1500 0.0000 -0.1500> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
        	]) to editable_mesh;
        
		add_patch of (new_bicubic_basis of [
            [<0.8500 -0.2500 0.4000> <1.1500 -0.1500 0.5000> <0.3000 0.1000 0.1000> <0.2228 0.0835 0.2228>] 
            [<0.8500 0.0000 0.1500> <1.2561 0.0000 0.3939> <0.4061 0.0000 0.2439> <0.2642 0.0000 0.3257>]
            [<0.0000 0.0000 -0.3536> <0.1500 0.0000 -0.1500> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>] 
            [<0.0000 0.3536 0.0000> <0.0000 0.2121 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
            ]) to editable_mesh;

        add_patch of (new_bicubic_basis of [
            [<0.8500 0.0000 0.1500> <1.2561 0.0000 0.3939> <0.4061 0.0000 0.2439> <0.2642 0.0000 0.3257>] 
            [<0.8500 0.2500 0.4000> <1.1500 0.1500 0.5000> <0.3000 -0.1000 0.1000> <0.2228 -0.0835 0.2228>] 
            [<0.0000 0.3536 0.0000> <0.0000 0.2121 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>] 
            [<0.0000 0.0000 0.3536> <-0.1500 0.0000 0.1500> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
            ]) to editable_mesh;
         
        add_patch of (new_bicubic_basis of [
            [<1.1500 0.1500 0.5000> <1.2500 0.1000 0.8000> <0.2228 -0.0835 0.2228> <0.1617 -0.0404 0.2156>]
            [<1.0439 0.0000 0.6061> <1.1634 0.0000 0.8500> <0.1983 0.0000 0.1265> <0.1676 0.0000 0.1610>]
            [<-0.1500 0.0000 0.1500> <-0.1225 0.0000 0.0707> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
            [<0.0000 -0.2121 0.0000> <0.0000 -0.1414 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
            ]) to editable_mesh;
            
        add_patch of (new_bicubic_basis of [
            [<1.0439 0.0000 0.6061> <1.1634 0.0000 0.8500> <0.1983 0.0000 0.1265> <0.1676 0.0000 0.1610>] 
            [<1.1500 -0.1500 0.5000> <1.2500 -0.1000 0.8000> <0.2228 0.0835 0.2228> <0.1617 0.0404 0.2156>] 
            [<0.0000 -0.2121 0.0000> <0.0000 -0.1414 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>] 
            [<0.1500 0.0000 -0.1500> <0.1225 0.0000 -0.0707> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
        	]) to editable_mesh;
        	
        add_patch of (new_bicubic_basis of [
            [<1.1500 -0.1500 0.5000> <1.2500 -0.1000 0.8000> <0.2228 0.0835 0.2228> <0.1617 0.0404 0.2156>]
            [<1.2561 0.0000 0.3939> <1.3366 0.0000 0.7500> <0.2642 0.0000 0.3257> <0.1572 0.0000 0.2706>] 
            [<0.1500 0.0000 -0.1500> <0.1225 0.0000 -0.0707> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>] 
            [<0.0000 0.2121 0.0000> <0.0000 0.1414 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
            ]) to editable_mesh;
            
        add_patch of (new_bicubic_basis of [
            [<1.2561 0.0000 0.3939> <1.3366 0.0000 0.7500> <0.2642 0.0000 0.3257> <0.1572 0.0000 0.2706>] 
            [<1.1500 0.1500 0.5000> <1.2500 0.1000 0.8000> <0.2228 -0.0835 0.2228> <0.1617 -0.0404 0.2156>] 
            [<0.0000 0.2121 0.0000> <0.0000 0.1414 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>] 
            [<-0.1500 0.0000 0.1500> <-0.1225 0.0000 0.0707> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
            ]) to editable_mesh;
             
        add_patch of (new_bicubic_basis of [
            [<1.2500 0.1000 0.8000> <1.4500 0.0750 0.9000> <0.1617 -0.0404 0.2156> <0.2000 -0.0250 0.1000>] 
            [<1.1634 0.0000 0.8500> <1.3500 0.0000 0.9000> <0.1676 0.0000 0.1610> <0.1866 0.0000 0.0500>] 
            [<-0.1225 0.0000 0.0707> <-0.1250 0.0000 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>] 
            [<0.0000 -0.1414 0.0000> <0.0000 -0.1250 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
            ]) to editable_mesh;
            
        add_patch of (new_bicubic_basis of [
            [<1.1634 0.0000 0.8500> <1.3500 0.0000 0.9000> <0.1676 0.0000 0.1610> <0.1866 0.0000 0.0500>] 
            [<1.2500 -0.1000 0.8000> <1.4500 -0.0750 0.9000> <0.1617 0.0404 0.2156> <0.2000 0.0250 0.1000>] 
            [<0.0000 -0.1414 0.0000> <0.0000 -0.1250 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>] 
            [<0.1225 0.0000 -0.0707> <0.1250 0.0000 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
            ]) to editable_mesh;
            
        add_patch of (new_bicubic_basis of [
            [<1.2500 -0.1000 0.8000> <1.4500 -0.0750 0.9000> <0.1617 0.0404 0.2156> <0.2000 0.0250 0.1000>] 
            [<1.3366 0.0000 0.7500> <1.5500 0.0000 0.9000> <0.1572 0.0000 0.2706> <0.2134 0.0000 0.1500>] 
            [<0.1225 0.0000 -0.0707> <0.1250 0.0000 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>] 
            [<0.0000 0.1414 0.0000> <0.0000 0.1250 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
            ]) to editable_mesh;
            
        add_patch of (new_bicubic_basis of [
            [<1.3366 0.0000 0.7500> <1.5500 0.0000 0.9000> <0.1572 0.0000 0.2706> <0.2134 0.0000 0.1500>] 
            [<1.2500 0.1000 0.8000> <1.4500 0.0750 0.9000> <0.1617 -0.0404 0.2156> <0.2000 -0.0250 0.1000>] 
            [<0.0000 0.1414 0.0000> <0.0000 0.1250 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>] 
            [<-0.1225 0.0000 0.0707> <-0.1250 0.0000 0.0000> <0.0000 0.0000 0.0000> <0.0000 0.0000 0.0000>]
            ]) to editable_mesh;
    end;    // add_spout
    
    			
	translated_3ds_stackable type question get_collapsed is
		translated_3ds_editable_mesh type editable_mesh is translated_3ds_editable_mesh type's get;
		trans type trans;
		
		if body then
			add_body to editable_mesh;
		end;
		if lid then
			add_lid to editable_mesh;
		end;
		if handle then
			add_handle to editable_mesh;
		end;
		if spout then
		    add_spout to editable_mesh;
		end;
		
		magnify the trans by radius;
		editable_mesh transform from trans;
		
		recycle;
		answer editable_mesh;
	end;	// get_collapsed
	
    	
	//	
	// recycling methods
	//
	
	
	objective translated_3ds_teapot type question get is
		if some free_list then
		
			// get from free list
			//
			translated_3ds_teapot type next is free_list;
			free_list is its next;
			
			answer next;
		else
			// create new
			//
			answer new translated_3ds_teapot;
		end;
	end;	// get
	
	
	reference verb recycle is
	
		// add to free list
		//
		next is free_list;
		free_list is translated_3ds_teapot;
		
		translated_3ds_teapot is none;
	end;		// recycle			
end;    // translated_3ds_teapot


//
// procedural interface
//


shape translated_3ds_teapot with

	// geometry parameters
	//
    scalar radius = 1;
    
    // tessellation parameters
    //
    integer segments = 4;
    boolean smooth is true;
        
    // part parameters
    //
    boolean body is true;
    boolean handle is true;
    boolean spout is true;
    boolean lid is true;
    
    // texturing parameters
    //
    boolean gen_mapping_coords is false;
    
    // modifier stack
    //
	translated_3ds_teapot type translated_3ds_teapot is translated_3ds_teapot type's get;
	modifier_stack push translated_3ds_stackable type translated_3ds_teapot;
is
	// set first entry in modifier stack to object
	//
	translated_3ds_teapot set with
	
		// set geometry parameters
		//
	    radius = static radius;
	    
	    // set tessellation parameters
	    //
	    segments = static segments;
	    smooth is static smooth;
	        
	    // set part parameters
	    //
	    body is static body;
	    handle is static handle;
	    spout is static spout;
	    lid is static lid;
	    
	    // set texturing parameters
	    //
	    gen_mapping_coords is static gen_mapping_coords;	  
	end;
	
	// render
	//
	modifier_stack instance;
end;	// translated_3ds_teapot
