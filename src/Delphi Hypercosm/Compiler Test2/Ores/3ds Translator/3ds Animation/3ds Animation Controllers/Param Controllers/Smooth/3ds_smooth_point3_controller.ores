{***************************************************************}
{ |\  /|												We Put  }
{ | >< Hypercosm   3ds_smooth_point3_controller.ores	3d      }
{ |/  \|												To Work }
{***************************************************************}
{                                                               }
{   	Contains controller definitions that correspond to 		}
{		entities from Discreet's 3D Studio Max software.		}
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}


include "system/3ds_bezier_point3_controller.ores";
include "system/statistics.ores";


subject translated_3ds_smooth_point3_controller 
extends
	translated_3ds_bezier_point3_controller
does
    // constructor
    //
	verb new
		of vector values[];
		at translated_3ds_time type times[];
	end;
is
	objective vector[] question get_tangents
		of vector values[];
		at translated_3ds_time type times[];
	is
		vector tangents[min values .. max values];
		vector slopes[1 .. num values - 1];
			
		// compute slopes
		//
		for integer counter = 1 .. num values - 1 do
			vector dy = values[counter + 1] - values[counter];			
			slopes[counter] = -dy;
		end;
			
		// compute tangents
		//
		for integer counter = 1 .. num values do
			if counter = 1 then
				tangents[counter] = slopes[counter];
			elseif counter = num values then
				tangents[counter] = slopes[counter - 1];
			else
				tangents[counter] = (slopes[counter - 1] + slopes[counter]) / 2;
			end;
		end;
			
		answer tangents;
	end;	// get_tangents
	

    //
    // constructor
    //
    
    	
	verb new
		of vector values[];
		at translated_3ds_time type times[];
	is
		vectors type in_tangents is (get_tangents of values at times);
		vectors type out_tangents is (in_tangents flipped);
		translated_3ds_bezier_point3_controller new  
			of values from in_tangents to out_tangents at times;
	end;	// new
end;	// translated_3ds_smooth_point3_controller




