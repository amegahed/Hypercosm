{***************************************************************}
{ |\  /|												We Put  }
{ | >< Hypercosm 		  3ds_circle.ores				3d      }
{ |/  \|												To Work }
{***************************************************************}
{                                                               }
{   	Contains circle definitions that correspond to 			}
{		entities from Discreet's 3D Studio Max software.		}
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}


include "system/3ds_shape.ores";
include "system/3ds_editable_spline.ores";
include "system/3ds_modifier_stacks.ores";


subject translated_3ds_circle
extends
    translated_3ds_collapsable translated_3ds_shape
does
    // constructor
    //
	verb new with

	    // geometry parameters
	    //
        scalar radius = 1;
        	
        // shape parameters
        //
        integer steps = 4;
        boolean optimize is false;
        boolean adaptive is false;
        boolean renderable is false;
        scalar thickness = 1;
        
        // texturing parameters
        //
        boolean gen_mapping_coords is false;
	end;
	
    // attribute setting method
    //
	verb set with

	    // geometry parameters
	    //
        scalar radius = 1;
        	
        // shape parameters
        //
        integer steps = 4;
        boolean optimize is false;
        boolean adaptive is false;
        boolean renderable is false;
        scalar thickness = 1;
        
        // texturing parameters
        //
        boolean gen_mapping_coords is false;
	end;
	
	// collapsable method
	//
	translated_3ds_stackable type question get_collapsed;
	
	// recycling methods
	//
	objective translated_3ds_circle type question get;
	reference verb recycle;	
has
    // geometry attributes
    //
    scalar radius;
    
private	
	// recycling attributes
	//
	translated_3ds_circle type next is none;
is
    // recycling free list
    //
    translated_3ds_circle type free_list is none;
    
    
    //
    // constructor
    //
    
    
	verb new with

	    // geometry parameters
	    //
        scalar radius = 1;
        
        // shape parameters
        //
        integer steps = 4;
        boolean optimize is false;
        boolean adaptive is false;
        boolean renderable is false;
        scalar thickness = 1; 
        
        // texturing parameters
        //
        boolean gen_mapping_coords is false;
	is
	    translated_3ds_shape new with
	    
			// set shape parameters
			//
            steps = static steps;
            optimize is static optimize;
            adaptive is static adaptive;
            renderable is static renderable;
            thickness = static thickness; 
            
            // texturing parameters
            //
            gen_mapping_coords is static gen_mapping_coords;	    
	    end;

		if verbose_recycling then
			write "new 3ds circle", ;
		end;
				    
		with translated_3ds_circle do
		
			// set geometry attributes
			//
            its radius = radius;
		end;
	end;	// new
	

	//
    // attribute setting method
    //
    
    
	verb set with

	    // geometry parameters
	    //
        scalar radius = 1;
        	
        // shape parameters
        //
        integer steps = 4;
        boolean optimize is false;
        boolean adaptive is false;
        boolean renderable is false;
        scalar thickness = 1;
        
        // texturing parameters
        //
        boolean gen_mapping_coords is false;
    is
		with translated_3ds_circle do
		
			// set geometry attributes
			//
            its radius = radius;
            
			// set shape parameters
			//
            its steps = steps;
            its optimize is optimize;
            its adaptive is adaptive;
            its renderable is renderable;
            its thickness = thickness; 
            
            // texturing parameters
            //
            its gen_mapping_coords is gen_mapping_coords;	
		end;    
	end;	// set
	
		
	//	
	// collapsable method
	//
	
	
	translated_3ds_stackable type question get_collapsed is
	    const integer num_segments = 4;
        translated_3ds_spline_vertices type vertices;
        translated_3ds_spline_segments type segments;
        translated_3ds_spline type spline is none;
        translated_3ds_splines type splines is none;
        translated_3ds_editable_spline type editable_spline is none;
        
        // geometric variables
        //
    	scalar angle = 360 / num_segments;
    	scalar a = (sin angle) * (sqrt (4 + 3 * sqr tan (angle / 2)) - 1);
        	
        // create vertices
        //
        dim none vertices[1 .. num_segments];
        for integer counter = 1 .. num vertices do
			scalar t = (counter - 1) / num vertices;
			scalar u = t * 360;
			scalar x = cos u;
			scalar y = sin u;
			vector point = <x y 0> * radius;
			vector tangent = <-y x 0> * radius * a;
			vertices[counter] is translated_3ds_bezier_vertex 
				at point from (-tangent / 3) to (tangent / 3);
        end;
        
        // create segments
        //
        dim none segments[1 .. num_segments];
        for integer counter = 1 .. num_segments - 1 do
            segments[counter] is translated_3ds_spline_segment type's get;
            segments[counter] set curve from vertices[counter] to vertices[counter + 1];
        end;
        segments[num_segments] is translated_3ds_spline_segment type's get;
        segments[num_segments] set curve from vertices[num_segments] to vertices[1];
        
        // create spline
        //
        spline is translated_3ds_spline type's get;
        spline append_vertices vertices;
        spline append_segments segments;
        
        // create splines
        //
        splines is [spline];
                
        // create editable spline
        //
        editable_spline is translated_3ds_editable_spline type's get;
        editable_spline set to splines with
        
            // set shape parameters
            //
            steps = static steps;
            optimize is static optimize;
            adaptive is static adaptive;
            renderable is static renderable;
            thickness = static thickness;
            
            // set texturing parameters
            // 
            gen_mapping_coords is static gen_mapping_coords;
        end;
                      
        recycle;      
        answer editable_spline;
	end;	// get_collapsed
	

	//	
	// recycling methods
	//
	
	
	objective translated_3ds_circle type question get is
		if some free_list then
		
			// get from free list
			//
			translated_3ds_circle type next is free_list;
			free_list is its next;
			
			answer next;
		else
			// create new
			//
			answer new translated_3ds_circle;
		end;
	end;	// get
	
	
	reference verb recycle is
	
		// add to free list
		//
		next is free_list;
		free_list is translated_3ds_circle;
		
		translated_3ds_circle is none;
	end;		// recycle	
end;	// translated_3ds_circle


//
// procedural interface
//


shape translated_3ds_circle with

    // geometry parameters
    //
    scalar radius = 1;
    
    // shape parameters
    //
    integer steps = 4;
    boolean optimize is false;
    boolean adaptive is false;
    boolean renderable is false;
    scalar thickness = 1; 
    
    // texturing parameters
    //
    boolean gen_mapping_coords is false;

    // modifier stack
    //
	translated_3ds_circle type translated_3ds_circle is translated_3ds_circle type's get;
	modifier_stack push translated_3ds_stackable type translated_3ds_circle;
is
	// set first entry in modifier stack to object
	//
	translated_3ds_circle set with

	    // set geometry parameters
	    //
        radius = static radius;
        
        // set shape parameters
        //
        steps = static steps;
        optimize is static optimize;
        adaptive is static adaptive;
        renderable is static renderable;
        thickness = static thickness;
        
        // set texturing parameters
        // 
        gen_mapping_coords is static gen_mapping_coords;
	end;
	
	// render
	//
	modifier_stack instance;
end;	// translated_3ds_circle
