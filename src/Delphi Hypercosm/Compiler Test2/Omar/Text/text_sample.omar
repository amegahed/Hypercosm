{***************************************************************}
{                        text_sample.omar                       }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************} 
{                                                               }
{       Contains sample code that demonstrates how to use       }
{       Hypercosm's standard text.                              }
{                                                               }
{***************************************************************} 
{                Copyright (c) 2000 Hypercosm, Inc.             }
{***************************************************************} 
do text_anim;


include "system/anims.ores";
include "system/text.ores";


shape words is
    text_style type style1 with
        color = orange;
        size = .75;
    end;

    text "plain & simple & centered";
    
    text "Text in a Parallelogram" with
        move to <0 1 5>;
        color = lavender;
    end;
    parallelogram with
        rotate by 90 around <1 0 0>;
        scale by (text_length of "Text in a Parallelogram")/2
            along <1 0 0>;
        scale by (1 + text_descent)/2 along <0 0 1>;
        move to <0 (1.02) (5 - (text_descent/2))>;
        color = dark dark green;
    end;
    text "Underlined Text" with
        underline is on;
        color = light brown;
        move to <0 -1.5 3>;
    end;

    line [<-3 0 2> <-3 0 1> <0 0 1> <0 0 -1> <3 0 -1> <3 0 -2>] with
        color = grey;
    end;
    text "left-aligned, bottom-aligned" using style1 with
        move to <-3 0 1>;
        h_align is left;
        v_align is bottom;
    end;    
    text "right-aligned, top-aligned" using style1 with
        move to <3 0 -1>;
        h_align is right;
        v_align is top;
    end;    
    text "SQUEEZED TEXT" with
        length = 8;
        move to <0 1.5 -3>;
        color = light turquoise;
    end;
    text "STRETCHED TEXT" with
        length = 18;
        move to <0 -1 -5>;
        color = light yellow;
    end;
end;    // words


anim text_anim with
    eye = <-5 -24 0>;
is
    mouse_controlled_shape words;
end;    // text_anim
