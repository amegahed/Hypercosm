{***************************************************************}
{                          microscope.omar                      }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2000 Hypercosm, Inc.             }
{***************************************************************}
do microscope_anim;


include "system/3d.ores";
include "system/anims.ores";
include "system/shapes.ores";


shape microscope is
    shape mirror_thing is
        paraboloid with
            top = <-1 0 -.1>;
            base = <-1 0 .1>;
            radius = .5;
            rotate by -20 around <0 1 0>;
        end;
        cylinder with
            end1 = <0 0 0>;
            end2 = <-.5 0 0>;
            radius = .1;
        end;
        cylinder with
            end1 = <-.5 -.5 0>;
            end2 = <-.5 .5 0>;
            radius = .1;
        end;
        cylinder with
            end1 = <-.5 -.5 0>;
            end2 = <-1 -.5 0>;
            radius = .1;
        end;
        cylinder with
            end1 = <-.5 .5 0>;
            end2 = <-1 .5 0>;
            radius = .1;
        end;
    end;    // mirror


    shape microscope_base is
        extrude [[1 1] [0 2] [-3 2] [-3 1] [-1 1] [-1 -1]
            [-3 -1] [-3 -2] [0 -2] [1 -1]]
        with
            rotate by 90 around <1 0 0>;
            move to <0 0 .5>;
        end;
        block with
            vertex = <-.5 -1 1>;
            side1 = <1 0 0>;
            side2 = <0 2 0>;
            side3 = <0 0 1>;
        end;
        mirror_thing with
            material is metal colored white;
            move to <-.5 0 1.5>;
        end;
        cylinder with
            end1 = <0 -1 2>;
            end2 = <0 1 2>;
            radius = .5;
        end;
        disk with
            center = <0 -1 2>;
            normal = <0 1 0>;
            radius = .5;
        end;
        disk with
            center = <0 1 2>;
            normal = <0 1 0>;
            radius = .5;
        end;
    end;    // microscope_base


    shape focusing_knob is
        cylinder with
            material is metal colored white;
            end1 = <0 -1.2 0>;
            end2 = <0 1.2 0>;
            radius = .2;
        end;
        disk with
            center = <0 -1.2 0>;
            normal = <0 1 0>;
            radius = .2;
        end;
        disk with
            center = <0 1.2 0>;
            normal = <0 1 0>;
            radius = .2;
        end;
        cylinder with
            material is rubber;
            end1 = <0 -1 0>;
            end2 = <0 -.8 0>;
            radius = .4;
        end;
        cylinder with
            material is rubber;
            end1 = <0 1 0>;
            end2 = <0 .8 0>;
            radius = .4;
        end;
        disk with
            material is chalk colored light grey;
            center = <0 -1 0>;
            normal = <0 1 0>;
            radius = .4;
        end;
        disk with
            material is chalk colored light grey;
            center = <0 1 0>;
            normal = <0 1 0>;
            radius = .4;
        end;
    end;    // focusing_knob


    shape objective_lens is
        cylinder with
            material is metal colored white;
            end1 = <0 0 0>;
            end2 = <0 0 -.5>;
            radius = .2;
        end;
        cone with
            material is metal colored white;
            end1 = <0 0 -.5>;
            end2 = <0 0 -.6>;
            radius1 = .2;
            radius2 = .05;
        end;
    end;    // objective_lens


    shape optical_assembly is
        cone with
            end1 = <-.5 0 0>;
                        end2 = <-.5 0 .5>;
                        radius1 = 1;
            radius2 = .6;
                        skew from <-.5 0 .5> to <0 0 .5> about <-.5 0 0>;
        end;    // objective lens skirt
        disk with
            center = <-.5 0 0>;
                        normal = <0 0 1>;
            radius = 1;
        end;
        objective_lens;
        objective_lens with
            rotate by 120 around <0 0 1> about <-.5 0 0>;
        end;
        objective_lens with
            rotate by -120 around <0 0 1> about <-.5 0 0>;
        end;

        // lower body
        //
        cylinder with
            end1 = <0 0 .5>;
            end2 = <0 0 4>;
            radius = .6;
        end;

        // highlighting stripes
        //
        cylinder with
            material is chalk colored white;
            end1 = <0 0 .5>;
            end2 = <0 0 .6>;
            radius = .65;
        end;
        cylinder with
            material is chalk colored white;
            end1 = <0 0 4>;
            end2 = <0 0 3.9>;
            radius = .65;
        end;
        cone with
            material is metal colored white;
            end1 = <0 0 4>;
            end2 = <0 0 4.1>;
            radius1 = .6;
            radius2 = .4;
        end;

        // upper body
        //
        cylinder with
            material is metal colored white;
            end1 = <0 0 4.1>;
            end2 = <0 0 6>;
            radius = .4;
        end;

        // top of eyepiece
        //
        cone with
            material is rubber;
            end1 = <0 0 6>;
            end2 = <0 0 6.1>;
            radius1 = .4;
            radius2 = .45;
        end;
        cylinder with
            material is rubber;
            end1 = <0 0 6.1>;
            end2 = <0 0 6.2>;
            radius = .45;
        end;
        ring with
            material is rubber;
            center = <0 0 6.2>;
            normal = <0 0 1>;
            inner_radius = .2;
            outer_radius = .45;
        end;
    end;    // optical_assembly


    shape microscope_body is
        block with
            vertex = <-.3 -.3 0>;
            side1 = <.6 0 0>;
            side2 = <0 .6 0>;
            side3 = <0 0 5>;
        end;    // neck
        focusing_knob with
            move to <0 0 4.5>;
        end;
        block with
            material is rubber;
            vertex = <0 -1.5 .5>;
            side1 = <-2.5 0 0>;
            side2 = <0 3 0>;
            side3 = <0 0 .3>;
        end;    // stage
        optical_assembly with
                        move to <-.8 0 1.6>;
        end;
    end;    // microscope_body

        color = grey;
    microscope_base;
    microscope_body with
        rotate by 15 around <0 1 0>;
        move to <0 0 2>;
    end;
end;    // microscope


picture microscope_picture with
    field_of_view = 50;
    eye = <0 -15 15>;
    lookat = <0 0 4>;
    projection is fisheye;
    render_mode is shaded;
    shading is vertex;
    background = grey;
    width = trunc (height * .75);
is
    distant_light from <.3 -1 .5>;
    distant_light from <-.3 -.5 1>;
    microscope with
        material is golden;
        rotate by 50 around <0 0 1>;
        move to <1 0 0>;
    end;
    plane with
        color = dark brown;
        magnify by 10;
    end;
end;    // microscope_picture


anim microscope_anim
    args type args;
with
    eye = <-10 -10 10>;
    lookat = <0 0 4>;
    double_buffer is on;
    width = 300;
    height = 400;
    // width = trunc (height * .75);
    args check;
is
    shape scene is
        microscope with
            material is golden;
        end;
        plane with
            magnify by 10;
            render_mode is wireframe;
        end;
    end;    // scene

    mouse_controlled_shape scene;
end;    // microscope_anim
