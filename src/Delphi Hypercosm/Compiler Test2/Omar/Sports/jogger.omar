{***************************************************************}
{                          jogger.omar                          }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2000 Hypercosm, Inc.             }
{***************************************************************}
do jogging_woman_anim;


include "system/3d.ores";
include "system/actors.ores";
include "system/anims.ores";


subject jogging_woman
extends
    timed_actor
does
    objective shape legs with
        scalar t = 0;
    end;    // legs

    objective shape form with
        scalar t = 0;
    end;    // form

    shape instance;
has
    public scalar steps_per_sec = 1;
is
    material type flesh_material is chalk colored flesh;
    material type hair_material is plastic colored brown;
    material type dress_material is plastic colored red;

    objective shape left_toes is
        sphere with
            center = <-1 1 -.2>;
            radius = .7;
            scale by 1.7 along <-2 1 0>;
        end;    // big toe
        sphere with
            center = <-1 .2 -.2>;
            radius = .5;
            scale by 2 along <-4 1 0>;
        end;    // toe #2
        sphere with
            center = <-1 -.2 -.2>;
            radius = .4;
            scale by 2 along <-4 -1 0>;
        end;    // toe #3
        sphere with
            center = <-.8 -.8 -.2>;
            radius = .4;
            scale by 1.8 along <-3 -1 0>;
        end;    // toe #4
        sphere with
            center = <-.6 -1.4 -.2>;
            radius = .4;
            scale by 1.5 along <-2 -1 0>;
        end;    // toe #5
    end;    // left_toes


    objective shape left_foot is
        left_toes with
            magnify by .8;
            rotate by 20 around <0 0 1>;
        end;    // left_toes
        sphere with
            center = <0 0 0>;
            radius = 1.5;
            scale by .5 along <0 0 1>;
        end;
        sphere with
            center = <3 0 0>;
            radius = 1;
            scale by .5 along <0 0 1>;
        end;
        cone with
            end1 = <0 0 0>;
            end2 = <3 0 0>;
            radius1 = 1.5;
            radius2 = 1;
            scale by .5 along <0 0 1>;
        end;
        sphere with
            radius = .8;
            scale by 1.5 along <1 0 0>;
            move to <3 0 -1>;
        end;    // heel
        sphere with
            radius = 1;
            scale by 2 along <1 0 0>;
            move to <2 0 .5>;
        end;    // top of foot
        sphere with
            center = <3 -.5 0>;
            radius = .9;
        end;    // ankle
        sphere with
            center = <3 .5 0>;
            radius = .9;
        end;    // ankle
    end;    // left_foot


    objective shape lower_left_leg with
        scalar foot_angle = 0;
    is
        sphere with
            center = <0 0 0>;
            radius = 1.25;
            scale by 2 along <0 0 1> about <0 0 1.5>;
        end;    // calf muscle
        cone with
            end1 = <0 0 -2>;
            end2 = <0 0 -8>;
            radius1 = 1.25;
            radius2 = .5;
        end;    // lower leg
        left_foot with
            magnify by .5;
            move to <-2 0 -8>;
            rotate by foot_angle around <0 1 0> about
                <0 0 -8>;
        end;
        sphere with
            center = <-.5 0 0>;
            radius = 1.2;
        end;    // knee
    end;    // lower_left_leg


    objective shape left_leg with
        scalar knee_angle = -50;
    is
        sphere with
            center = <0 0 0>;
            radius = 1.8;
                        // scale by 1.5 along <0 0 1>;
        end;    // thigh muscle
        cone with
            end1 = <0 0 0>;
            end2 = <0 0 -5.75>;
            radius1 = 1.8;
            radius2 = 1.2;
        end;    // lower leg
        lower_left_leg with
            foot_angle = knee_angle + 30;
            rotate by knee_angle around <0 1 0>;
            move to <0 0 -5.75>;
        end;
    end;    // left_leg


    objective shape legs with
        scalar t = 0;
    is
        scalar angle1, angle2;
        scalar x;

        x = sin (t * 360);
        if (x < 0) then
            angle1 = x * x * 80;
        else
            angle1 = x * sin (x * 90) * 80;
        end;
        x = sin (t * 360 + 90);
        if (x < 0) then
            angle2 = x * x * 80;
        else
            angle2 = x * sin (x * 90) * 80;
        end;
        angle1 = angle1 - 60;
        angle2 = angle2 - 60;
        x = (angle1 * .7) - 20;
        if x > 0 then
            x = -x;
        end;
        left_leg with
            knee_angle = x;
            scale by -.75 along <0 1 0>;
            rotate by angle1 around <0 1 0> about <0 0 .5>;
            move to <0 -.75 0>;
        end;    // left leg
        x = (angle2 * .7) - 20;
        if x > 0 then
            x = -x;
        end;
        left_leg with
            knee_angle = x;
            scale by -.75 along <0 1 0>;
            rotate by angle2 around <0 1 0> about <0 0 .5>;
            move to <0 1 0>;
        end;    // right leg
    end;    // legs


    objective shape head is
        sphere with
            material is flesh_material;
            radius = 1.5;
            scale by 1.2 along <0 0 1>;
        end;
        sphere with
            material is hair_material;
            radius = 2;
            scale by .5 along <0 0 1>;
            rotate by 30 around <0 1 0>;
            scale by .75 along <0 1 0>;
            move to <0 0 1>;
        end;
        cylinder with
            material is flesh_material;
            end1 = <0 0 -1>;
            end2 = <0 0 -3>;
            radius = .7;
        end;
        cone with
            material is hair_material;
            end1 = <0 0 .5>;
            end2 = <3 0 -2>;
            radius1 = 1.2;
            radius2 = 1.7;
            scale by .4 along <1 0 0>;
            move to <.5 0 0>;
        end;    // hair
    end;    // head


    objective shape forearm is
        sphere with
            center = <0 0 0>;
            radius = .5;
        end;
        cone with
            end1 = <0 0 0>;
            end2 = <0 0 -4>;
            radius1 = .5;
            radius2 = .35;
        end;
        sphere with
            center = <0 0 -4>;
            radius = .7;
            scale by .5 along <0 1 0>;
        end;
    end;    // forearm


    objective shape arm with
        scalar bend = 0;
    is
        sphere with
            center = <0 0 0>;
            radius = 1;
        end;
        cone with
            end1 = <0 0 0>;
            end2 = <0 0 -5>;
            radius1 = 1;
            radius2 = .5;
        end;
        forearm with
            rotate by (bend + 30) around <0 1 0>;
            move to <0 0 -5>;
        end;
    end;    // arm


    objective shape chest with
        scalar t = 0;
    is
        scalar angle;

        angle = sin (t * 360 * 2) * 40;
        cone with
            end1 = <0 0 0>;
            end2 = <0 1 3>;
            radius1 = 1.3;
            radius2 = 2.1;
        end;
        cone with
            end1 = <0 0 0>;
            end2 = <0 -1 3>;
            radius1 = 1.3;
            radius2 = 2.1;
        end;
        cone with
            end1 = <0 1 3>;
            end2 = <0 0 5>;
            radius1 = 2.1;
            radius2 = 1.2;
        end;
        cone with
            end1 = <0 -1 3>;
            end2 = <0 0 5>;
            radius1 = 2.1;
            radius2 = 1.2;
        end;
        head with
            move to <0 0 8.5>;
        end;
        arm with
            material is flesh_material;
            bend = angle;
            rotate by 15 around <1 0 0>;
            rotate by angle around <0 1 0>;
            move to <0 2.25 4>;
        end;
        arm with
            material is flesh_material;
            bend = -angle;
            rotate by -15 around <1 0 0>;
            rotate by -angle around <0 1 0>;
            move to <0 -2.25 4>;
        end;
    end;    // chest


    objective shape waist with
        scalar t = 0;
    is
        scalar x;

        x = sin (t * 360 * 4) * -5 - 10;
        cone with
            end1 = <0 -1 0>;
            end2 = <0 0 4>;
                        radius1 = 2;
            radius2 = 1.4;
        end;
        cone with
            end1 = <0 1 0>;
            end2 = <0 0 4>;
                        radius1 = 2;
            radius2 = 1.4;
        end;
        chest with
            t = static t;
            rotate by x around <0 1 0>;
            move to <0 0 4>;
        end;
    end;    // waist


    objective shape form with
        scalar t = 0;
    is
        scalar z = sqr (cos (t * 360 * 2 + 40)) * 2 + 14;

        legs with
            material is flesh_material;
            t = static t;
            move to <0 0 z>;
        end;
        waist with
            t = static t;
            move to <0 0 z>;
        end;
    end;    // form


    shape instance is
        form with
            material is dress_material;
            t = time * steps_per_sec / 2;
        end;
    end;    // instance
end;    // jogging_woman


anim jogging_woman_anim
    args type args;
with
    eye = <-30 -40 30>;
    lookat = <0 0 15>;
    double_buffer is on;
    args check;
is
    jogging_woman type jogging_woman;

    shape environment is
        plane with
            magnify by 10;
                        render_mode is wireframe;
        end;
    end;    // environment

    mouse_controlled_actor jogging_woman in environment;
end;    // jogging_woman_anim
