{***************************************************************}
{                          ant.omar                             }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2000 Hypercosm, Inc.             }
{***************************************************************}
do ant_anim;


include "system/3d.ores";
include "system/anims.ores";
include "system/common_materials.ores";


shape ant is
    shape pincer is
        cone with
            end1 = <0 0 0>;
            end2 = <0 0 -2>;
            radius1 = 1;
            radius2 = .5;
            skew from <0 0 -2> to <1.5 0 -2>;
        end;
        cone with
            end1 = <1.5 0 -2>;
            end2 = <1.5 0 -3>;
            radius1 = .5;
            radius2 = 0;
            skew from <1.5 0 -3> to <0 0 -3> about <1.5 0 -2>;
        end;
    end;    // pincer


    shape pincers is
        pincer with
            scale by .5 along <0 1 0>;
            rotation of <0 -10 90>;
        end;    // 10 degrees open
        pincer with
            scale by -1 along <1 0 0>;
            scale by .5 along <0 1 0>;
            rotation of <0 10 90>;
        end;    // 10 degrees open
    end;    // pincers


    shape antenna is
        cylinder with
            material is aluminum;
            end1 = <0 0 0>;
            end2 = <0 -1 1.5>;
            radius = .05;
        end;
        cylinder with
            material is aluminum;
            end1 = <0 -1 1.5>;
            end2 = <0 -2 1>;
            radius = .05;
        end;
        sphere with
            center = <0 -2 1>;
            radius = .1;
        end;
    end;    // antenna


    shape head is
        sphere with
            radius = .8;
            scale by 1.2 along <1 0 .5>;
        end;
        sphere with
            material is metal colored white;
            radius = .3;
            scale by 1.3 along <.5 0 1>;
            move to <-.4 .5 .4>;
        end;    // eye
        sphere with
            material is metal colored white;
            radius = .3;
            scale by 1.3 along <.5 0 1>;
            move to <-.4 -.4 .4>;
        end;    // eye
        antenna with
            rotate by -60 around <0 0 1>;   // rotate forward
            move to <-.1 -.1 .6>;
        end;
        antenna with
            scale by -1 along <0 1 0>;
            rotate by 40 around <0 0 1>;    // rotate forward
            move to <-.1 .1 .6>;
        end;
        pincers with
            rotation of <0 30 0>;
            magnify by .3;
            move to <-.7 0 -.5>;
        end;
    end;    // head


    shape leg is
        cylinder with
            material is plastic colored grey;
            end1 = <0 0 0>;
            end2 = <0 -1.5 1>;
            radius = .07;
        end;
        cylinder with
            material is plastic colored grey;
            end1 = <0 -1.5 1>;
            end2 = <0 -2.5 -1>;
            radius = .07;
        end;
        cylinder with
            material is plastic colored grey;
            end1 = <0 -2.5 -1>;
            end2 = <0 -3 -1>;
            radius = .07;
        end;
    end;    // leg


    shape rear_legs is
        leg with
            magnify by 1.2;
            rotate by 50 around <0 0 1>;
            move to <0 -1 0>;
        end;
        leg with
            magnify by 1.2;
            scale by -1 along <0 1 0>;
            rotate by -50 around <0 0 1>;
            move to <0 1 0>;
        end;
        sphere with
            radius = .5;
            scale by 2 along <0 1 0>;
        end;
    end;    // rear_legs


    shape legs is
        leg with
            magnify by .8;
            rotate by 10 around <0 0 1>;
            scale by 1.25 along <0 0 1>;
            move to <0 -1 0>;
        end;
        leg with
            magnify by .8;
            scale by -1 along <0 1 0>;
            rotate by -10 around <0 0 1>;
            scale by 1.25 along <0 0 1>;
            move to <0 1 0>;
        end;
        sphere with
            radius = .5;
            scale by 2 along <0 1 0>;
        end;
    end;    // legs


    shape front_legs is
        leg with
            magnify by .9;
            rotate by -40 around <0 0 1>;
            move to <0 -1 0>;
        end;
        leg with
            magnify by .9;
            scale by -1 along <0 1 0>;
            rotate by 40 around <0 0 1>;
            move to <0 1 0>;
        end;
        sphere with
            radius = .5;
            scale by 2 along <0 1 0>;
        end;
    end;    // front_legs


    shape body is
        sphere with
            scale by 1.6 along <1 0 0>;
            rotate by -20 around <0 1 0>;
            move to <-.5 0 0>;
        end;
        sphere with
            scale by 1.6 along <1 0 0>;
            rotate by 20 around <0 1 0>;
            move to <.5 0 0>;
        end;
        rear_legs with
            move to <1 0 -1>;
        end;
        legs with
            move to <0 0 -1>;
        end;
        front_legs with
            move to <-1.5 0 -1>;
        end;
    end;    // body


    shape abdomen is
        sphere with
            radius = 1.5;
            rotate by 90 around <0 1 0>;
            stretch by 1.4 along <1 0 0>;
            move to <1.5 0 0>;
        end;
    end;    // abdomen


    shape stuff is
        head with
            move to <-2.5 0 .3>;
        end;
        body;
        abdomen with
            rotate by -20 around <0 1 0>;
            move to <1.8 0 0>;
        end;
    end;    // stuff

    stuff with
        rotate by -3 around <0 1 0>;
        move to <0 0 2.1>;
    end;
end;    // ant


anim ant_anim
    args type args;
with
    eye = <-10 -5 5>;
    lookat = <0 0 1>;
    facets = 6;
    double_buffer is on;
    args check;
is
    shape scene is
        ant with
            material is plastic colored red;
        end;
        plane with
            magnify by 5;
            render_mode is wireframe;
        end;
    end;    // scene

    mouse_controlled_shape scene;
end;    // ant_anim
