{***************************************************************}
{                      mouse_controls.omar                      }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2000 Hypercosm, Inc.             }
{***************************************************************}
do thing_anim;


include "system/3d.ores";
include "system/shapes.ores";
include "system/text.ores";
include "system/time.ores";
include "system/arrow_shapes.ores";


shape thing is
    block;

    cylinder with
        end1 = <-1.5 0 0>;
        end2 = <1.5 0 0>;
        radius = .5;
    end;

    axes with
        magnify by 1.5;
    end;
end;    // thing


picture thing_picture with
    scalar x_spin = 180 * get_mouse dot <0 1 0>;
    scalar z_spin = 180 * get_mouse dot <1 0 0>;
is
    distant_light from <.3 -.5 1>;

    axes with
        magnify by 2.5;
    end;

    overlay_text "x-axis rotation:" at <-.95 .9 0> with
        h_align is left;
    end;
    overlay_text (scalar_to_string of x_spin to 2) at <-.5 .9 0> with
        h_align is left;
    end;
    overlay_text "z-axis rotation:" at <-.95 .8 0> with
        h_align is left;
    end;
    overlay_text (scalar_to_string of z_spin to 2) at <-.5 .8 0> with
        h_align is left;
    end;

    thing with
        rotate by z_spin around <0 0 1>;
        rotate by x_spin around <-1 0 0>;
    end;
end;    // thing_picture


anim thing_anim with
    double_buffer is on;
    eye = <0 -8 0>;
is
    while true do
        thing_picture;
    end;
end;    // thing_anim
