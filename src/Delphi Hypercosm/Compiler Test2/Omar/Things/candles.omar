{***************************************************************}
{                          candles.omar                         }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2000 Hypercosm, Inc.             }
{***************************************************************}
do candle_anim;


include "system/3d.ores";
include "system/anims.ores";
include "system/shapes.ores";
include "system/surfaces.ores";
include "system/shaders.ores";


// integer image1 = load_image "plank.tga";
shape candle is
    shape flame is
        sphere with
            center = <0 0 0>;
            radius = 3;
        end;
        paraboloid with
            top = <0 4 6>;
            base = <0 0 1>;
            radius = 2.5;
            skew from <0 4 6> to <0 2 6> about <0 2 2>;
        end;

        {
                cone with
                        end1 = 0 1.5 5.4; end2 = 0 2 9;
                        radius1 = 1.1; radius2 = 0;
                        skew from 0 2 9 to 0 -1 9 about 0 1.5 5.4;
                end;
                }

        point_light with
            move to <0 0 10>;
            color = light yellow;
                        brightness = 20;
        end;
    end;    // flame


    shape stick is
        cone with
            end1 = <0 0 -10>;
            end2 = <0 0 -50>;
            radius1 = 2;
            radius2 = 3;
        end;
        paraboloid with
            top = <0 0 -5>;
            base = <0 0 -10>;
            radius = 2;
        end;
        cylinder with
            end1 = <0 0 0>;
            end2 = <0 0 -5>;
            radius = .5;
        end;
    end;    // stick


    shape candlestick is
        fuzzy (gold * 3) flame with
            magnify by .8;
            move to <0 0 50>;
        end;

        {
                foggy orange sphere with
                        radius = 10;
                        move to 0 0 50;
                end;
                }

        {
                fuzzy orange sphere with
                        radius = 3;
                        move to 0 0 50;
                end;
                }
        stick with
            move to <0 0 50>;
        end;
    end;    // candlestick


    shape leaf is
        paraboloid with
            top = <0 0 2>;
            base = <0 0 0>;
            radius = 1;
            scale by .3 along <0 1 0>;
        end;
    end;    // leaf


    shape holder_top is
        disk with
            center = <0 0 0>;
            normal = <0 0 1>;
            radius = 10;
        end;
        disk with
            center = <0 0 -1>;
            normal = <0 0 1>;
            radius = 10;
        end;
        cylinder with
            end1 = <0 0 0>;
            end2 = <0 0 -1>;
            radius = 10;
        end;
        for integer counter = 1..10 do
            leaf with
                magnify by 3.1415;
                rotate by 90 around <0 0 1>;
                rotate by 35 around <0 1 0>;
                move to <10 0 0>;
                rotate by counter / 10 * 360 around
                    <0 0 1>;
            end;
        end;
    end;    // holder_top


    shape leg is
        extrude [[0 0] [2 -1] [3 -3] [4 -6] [2.25 -6] [1.4 -3.7]
            [0 -2.3] [0 -1.5] [1.8 -3.2] [2.75 -5.5]
            [3.25 -5.5] [2.5 -3.3] [1.75 -1.75] [0 -.8]]
        with
            scale by .5 along <0 1 0>;
        end;
    end;    // leg


    shape holder is
        cone with
            end1 = <0 0 0>;
            end2 = <0 0 5>;
            radius1 = 3;
            radius2 = 5.5;
        end;
        cone with
            end1 = <0 0 0>;
            end2 = <0 0 5>;
            radius1 = 3.8;
            radius2 = 5.5;
        end;
        holder_top;
        cylinder with
            end1 = <0 0 0>;
            end2 = <0 0 -15>;
            radius = 5;
        end;
        leg with
            move to <0 1 -2> / 5;
            magnify by 5;
        end;
        leg with
            move to <0 1 -2> / 5;
            magnify by 5;
            rotate by 120 around <0 0 1>;
        end;
        leg with
            move to <0 1 -2> / 5;
            magnify by 5;
            rotate by -120 around <0 0 1>;
        end;
    end;    // holder

    candlestick with
        material is chalk colored white;
        move to <0 0 32>;
    end;
    holder with
        material is copper;
        move to <0 0 32>;
    end;
end;    // candle


picture candle_picture with
    field_of_view = 50;
    eye = <0 -16 15>;
    lookat = <0 0 3>;
    render_mode is shaded;
    shading is pixel;
    facets = 0;
    reflections is on;
    refractions is on;
    shadows is on;
    width = trunc (height * .75);
is
    candle with
        magnify by .1;
        move to <3 0 0>;
        rotate by 35 around <0 0 1>;
    end;
    candle with
        magnify by .1;
        rotate by 50 around <0 0 1>;
        move to <3 0 0>;
        rotate by 120 around <0 0 1>;
    end;
    candle with
        magnify by .1;
        rotate by 75 around <0 0 1>;
        move to <3 0 0>;
        rotate by -120 around <0 0 1>;
    end;

    plane with
        magnify by 5;
        rotate the shader_trans by 90 around <0 0 1>;
        magnify the shader_trans by 30;
    end;
end;    // candle_picture


anim candle_anim with
    field_of_view = 50;
    eye = <0 -16 15>;
    lookat = <0 0 3>;
    width = trunc (height * .75);
    double_buffer is on;
is
    mouse_controlled_picture candle_picture;
end;    // candle_anim
