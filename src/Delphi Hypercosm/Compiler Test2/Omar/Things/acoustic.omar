{***************************************************************}
{                          acoustic.omar                        }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2000 Hypercosm, Inc.             }
{***************************************************************}
do acoustic_anim;


include "system/3d.ores";
include "system/anims.ores";
include "system/shapes.ores";


shape acoustic_guitar is
    shape tuning_knob is
        cylinder with
            end1 = <0 0 0>;
            end2 = <0 0 .5>;
            radius = .03;
        end;
        sphere with
            center = <0 0 .5>;
            radius = .08;
            scale by .2 along <0 0 1> about <0 0 .5>;
        end;
    end;    // tuning_knob

    shape lower_body is
        cylinder with
            end1 = <0 -.01 0>;
            end2 = <0 .8 0>;
            radius = 1;
        end;
        disk with
            center = <0 .8 0>;
            normal = <0 1 0>;
            radius = 1;
        end;
        cylinder with
            end1 = <1.5 0 0>;
            end2 = <1.5 .8 0>;
            radius = 1;
            scale by 1.3 along <0 0 1> about <1.5 0 0>;
        end;
        disk with
            center = <1.5 .8 0>;
            normal = <0 1 0>;
            radius = 1;
            scale by 1.3 along <0 0 1> about <1.5 0 0>;
        end;
    end;    // lower_body


    shape upper_body is
        ring with
            normal = <0 1 0>;
            move to <0 -.01 0>;
            inner_radius = .5;
            outer_radius = 1;
        end;
        disk with
            center = <1.5 0 0>;
            normal = <0 1 0>;
            radius = 1;
            scale by 1.3 along <0 0 1> about <1.5 0 0>;
        end;
        rod with
            material is wood_veneer;
            end1 = <-.5 0 0>;
            end2 = <-3.0 0 0>;
            radius = .1;
            scale by 2 along <0 0 1>;
        end;    // upper_neck of guitar
        rod with
            material is wood_veneer;
            end1 = <.5 0 0>;
            end2 = <1.5 0 0>;
            radius = .1;
            scale by 2 along <0 0 1>;
        end;    // lower_neck of guitar

        // finger board
        //
        extrude [[.2 0] [.4 .3] [.4 1] [-.4 1] [-.4 .3] [-.2 0]] with
            scale by .2 along <0 1 0>;
            rotate by -90 around <0 1 0>;
            move to <-2.9 0 0>;
        end;
    end;    // upper_body


    shape strings is
        line [<-3.7 -.15 .1> <1.2 -.15 .1>] with
            material is constant_color white;
        end;
        line [<-3.5 -.15 0> <1.2 -.15 0>] with
            material is constant_color white;
        end;
        line [<-3.3 -.15 -.1> <1.2 -.15 -.1>] with
            material is constant_color white;
        end;
    end;    // strings


    shape tuning_knobs is
        tuning_knob with
            move to <-3.7 0 .4>;
        end;
        tuning_knob with
            move to <-3.5 0 .4>;
        end;
        tuning_knob with
            move to <-3.3 0 .4>;
        end;
    end;    // tuning_knobs

    shape string_rest is
        cylinder with
            end1 = <1.5 0 .4>;
            end2 = <1.5 0 -.4>;
            radius = .15;
        end;
        disk with
            center = <1.5 0 .4>;
            normal = <0 0 1>;
            radius = .15;
        end;
        disk with
            center = <1.5 0 -.4>;
            normal = <0 0 1>;
            radius = .15;
        end;
    end;    // string_rest


    lower_body with
        material is plastic colored dark grey;
    end;
    upper_body with
        material is wood;
    end;
    string_rest with
        material is plastic colored dark grey;
    end;
    strings with
        color = white;
    end;
    tuning_knobs with
        material is golden;
    end;
end;    // acoustic_guitar


anim acoustic_anim
    args type args;
with
    eye = <2 -8 4>;
    lookat = <-.5 0 0>;
    double_buffer is on;
    facets = 8;
    args check;
is
    mouse_controlled_shape acoustic_guitar;
end;    // acoustic_anim
