do tactile_saturn_anim;


include "system/3d.ores";
include "system/tactile_anims.ores";
include "system/grid_meshes.ores";
include "system/grids.ores";


anim tactile_saturn_anim
    args type args;
with
    eye = <0 -15 15>;
    lookat = <0 0 2>;
    double_buffer is on;
    background = dark blue;
    ambient = white * .5;
    args check;
is
    material type saturn_material is chalk poster "saturn.jpg";
    material type ring_material is (new material type poster "rings.jpg" with
        specular = white;
        diffuse = white;
        ambient = white;
        emissive = white;
    );

    shape rings is
        write "making rings", ;
        quad_mesh of (disk_grid 1 by 20 with rmin = 1.25; rmax = 2.2) with
            material is ring_material;
            mending is on;
            closed is false;
            smoothing is on;
        end;
    end;
    
    shape planet is
        write "making planet", ;
        quad_mesh of (globe_grid 10 by 20) with
            material is saturn_material;
            mending is on;
            closed is true;
            smoothing is on;
        end;
    end;    // planet

    shape saturn is
        planet;
        rings;
        rings with
            move to <0 0 .01>;
        end;
    end;    // saturn
        
    shape saturn_shape is
        write "making saturn", ;
        saturn with
            move to <0 0 1>;
            magnify by 2;
        end;
    end;    // saturn_shape
    
    
    shape floor is
        plane with
            magnify by 10;
            render_mode is wireframe;
            color = green;
        end;
    end;    // floor

    tactile_mouse_controlled_shape saturn_shape titled "Tactile Saturn" in floor;
end;    // tactile_saturn_anim
