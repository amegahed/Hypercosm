{***************************************************************}
{                        spotlight.omar                         }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2000 Hypercosm, Inc.             }
{***************************************************************}
do spotlight_anim;


include "system/3d.ores";
include "system/anims.ores";
include "system/shapes.ores";
include "system/figures.ores";
include "system/arrow_shapes.ores";
include "system/futura_l.omar";


shape angle_marker
    scalar angle;
is
    scalar a = tan (angle / 2);

    line [<0 0 0> (<a 0 1>)];
    line [<0 0 0> (<(-a) 0 1>)];
    arrow_arc to angle with
        rotate by -angle / 2 around <0 0 1>;
        rotate by 90 around <0 0 1>;
        rotate by 90 around <1 0 0>;
    end;
    futura_l_word "ANGLE" with
        rotate by -90 around <1 0 0>;
        move to <0 0 .85>;
    end;
end;    // angle_marker


shape spotlight with
    vector direction = <0 0 -1>;
    scalar size = .5;
    scalar angle = 60;
is
    scalar a = tan (angle / 2);

    spot_light towards direction with
        brightness = 2;
        angle = static angle;
    end;
    cone with
        material is constant_color white;
        color = black;
        end1 = <0 0 .01>;
        end2 = (normalized direction) * size;
        radius1 = 0;
        radius2 = size * a;
    end;
    arrow from <0 0 0> to direction * .5;
    angle_marker angle with
        direct from <0 0 1> to direction;
    end;
end;    // spotlight


anim spotlight_anim with
    eye = <2 -8 4>;
    field_of_view = 30;
    lookat = <0 0 .25>;
    render_mode is shaded;
    shading is vertex;
is
	shape scene is
	    vector v = <.25 0 .5>;
	
	    spotlight with
	        size = .1;
	        move to <0 0 1>;
	        direction = <-.5 -.5 -1>;
	        rotate by 90 around direction about <0 0 1>;
	    end;
	    futura_l_word "SPOT LIGHT" with
	        rotate by 90 around <1 0 0>;
	        move to <0 0 1.1>;
	    end;
	    plane with
	        material is plastic colored grey polished semi shiny;
	        magnify by 1.25;
	    end;
	    sphere with
	        material is plastic colored red polished semi shiny;
	        center = <0 0 .25>;
	        radius = .25;
	    end;
    end;
    mouse_controlled_shape scene;
end;    // spotlight_anim
