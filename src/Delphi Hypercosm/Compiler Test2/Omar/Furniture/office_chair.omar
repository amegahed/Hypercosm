{***************************************************************}
{                          office_chair.omar                    }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2000 Hypercosm, Inc.             }
{***************************************************************}
do chair_anim;


include "system/3d.ores";
include "system/anims.ores";
include "system/shapes.ores";


shape office_chair is
    shape wheel_base is
        disk with
            center = <0 -.09 0>;
            normal = <0 1 0>;
            radius = .1;
        end;
        disk with
            center = <0 .09 0>;
            normal = <0 1 0>;
            radius = .1;
        end;
        parallelogram with
            vertex = <.1 -.09 0>;
            side1 = <-.2 0 0>;
            side2 = <0 0 .3>;
        end;
        parallelogram with
            vertex = <.1 .09 0>;
            side1 = <-.2 0 0>;
            side2 = <0 0 .3>;
        end;
        parallelogram with
            vertex = <.1 -.09 .3>;
            side1 = <0 .18 0>;
            side2 = <-.2 0 0>;
        end;
        cylinder with
            end1 = <0 -.11 0>;
            end2 = <0 .11 0>;
            radius = .02;
        end;
    end;    // wheel_base


    shape wheel is
        cylinder with
            end1 = <0 -.05 0>;
            end2 = <0 .05 0>;
            radius = .2;
        end;
        disk with
            center = <0 -.05 0>;
            normal = <0 1 0>;
            radius = .2;
        end;
        disk with
            center = <0 .05 0>;
            normal = <0 -1 0>;
            radius = .2;
        end;
        wheel_base with
            material is aluminum;
        end;
    end;    // wheel


    shape chair_hub_arm is
        cylinder with
            end1 = <0 0 0>;
            end2 = <0 1.3 0>;
            radius = .1;
        end;
        block with
            vertex = <.1 1.3 0>;
            side1 = <-.2 0 0>;
            side2 = <0 -.2 0>;
            side3 = <0 0 -.1>;
        end;
        sphere with
            center = <0 1.3 0>;
            radius = .1;
        end;
        wheel with
            material is chalk colored charcoal;
            move to <0 1.20 -.4>;
        end;
    end;    // chair_hub_arm


    shape chair_base with
        integer arms = 5;
    is
        // hub
        //
        disk with
            center = <0 0 .1>;
            normal = <0 0 1>;
            radius = .25;
        end;
        disk with
            center = <0 0 -.1>;
            normal = <0 0 1>;
            radius = .25;
        end;
        cylinder with
            end1 = <0 0 .1>;
            end2 = <0 0 -.1>;
            radius = .25;
        end;

        // arms
        //
        for integer counter = 1..arms do
            chair_hub_arm with
                rotate by (counter / arms) * 360 around
                    <0 0 1>;
            end;
        end;
    end;    // chair_base


    shape chair_seat is
        rounded_rod with
            end1 = <0 .5 0>;
            end2 = <0 -.5 0>;
            radius = 1;
            scale by .2 along <0 0 1>;
        end;
    end;    // chair_seat


    shape chair_back is
        rounded_rod with
            end1 = <0 .4 0>;
            end2 = <0 -.4 0>;
            radius = .5;
            scale by .2 along <1 0 0>;
        end;
    end;    // chair_back


    shape chair_connector is
        extrude [[-1 0] [-1.2 .2] [-1.2 1.7] [-.9 1.7] [-.9 1.6]
            [-1.1 1.6] [-1.1 .2] [-1 .1] [0 .1] [0 0]
            [-1 0]];
    end;    // chair_connector

    // chair parts
    //
    move to <0 0 .6>;
    chair_base;
    cylinder with
        material is aluminum;
        end1 = <0 0 0>;
        end2 = <0 0 1.5>;
        radius = .1;
    end;
    cylinder with
        material is aluminum;
        end1 = <0 0 0>;
        end2 = <0 0 .4>;
        radius = .2;
    end;
    disk with
        material is aluminum;
        center = <0 0 .4>;
        normal = <0 0 1>;
        radius = .2;
    end;
    chair_seat with
        move to <0 0 1.5>;
    end;
    chair_connector with
        material is aluminum;
        scale by .3 along <0 1 0>;
        move to <0 0 1>;
    end;
    chair_back with
        magnify by 1.3;
        move to <-.9 0 3>;
    end;
end;    // office_chair


anim chair_anim
    args type args;
with
    eye = <4 -8 4>;
    lookat = <0 0 2>;
    double_buffer is on;
    args check;
is
    shape scene is
        office_chair with
            material is chalk colored brown;
        end;
        plane with
            magnify by 5;
            render_mode is wireframe;
        end;
    end;    // scene

    mouse_controlled_shape scene;
end;    // chair_anim
