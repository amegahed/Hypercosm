{***************************************************************}
{                          coffee_table.omar                    }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2000 Hypercosm, Inc.             }
{***************************************************************}
do coffee_table_anim;


include "system/3d.ores";
include "system/anims.ores";
include "system/materials.ores";


shape coffee_table with
    scalar width = 10;
    scalar length = 20;
    scalar height = 7;
is
    shape cube is
        block with
            vertex = <0 0 0>;
            side1 = <1 0 0>;
            side2 = <0 1 0>;
            side3 = <0 0 1>;
        end;
    end;    // cube


    shape table is

        // coffee_table top
        //
        cube with
            scale by length along <0 1 0>;
            scale by width along <1 0 0>;
        end;

        // left side legs
        //
        cube with
            scale by -(height - 1) along <0 0 1>;
            move to <.5 1 0>;
        end;
        cube with
            scale by -(height - 1) along <0 0 1>;
            move to <(width - 1.5) 1 0>;
        end;

        // right side legs
        //
        cube with
            scale by -(height - 1) along <0 0 1>;
            move to <.5 (length - 2) 0>;
        end;
        cube with
            scale by -(height - 1) along <0 0 1>;
            move to <(width - 1.5) (length - 2) 0>;
        end;

        // cross bars
        //
        cube with
            scale by (width - 3) along <1 0 0>;
            move to <1.5 1 -(height - 1)>;
        end;
        cube with
            scale by (width - 3) along <1 0 0>;
            move to <1.5 (length - 2) -(height - 1)>;
        end;
        cube with
            scale by (width - 3) along <1 0 0>;
            move to <1.5 1 -(height/2)>;
        end;
        cube with
            scale by (width - 3) along <1 0 0>;
            move to <1.5 (length - 2) -(height/2)>;
        end;
        cube with
            scale by (length - 4) along <0 1 0>;
            move to <(width/2 - .5) 2 -(height/2)>;
        end;
    end;    // table

    table with
        move to <-(width/2) -(length/2) (height - 1)>;
    end;
end;    // coffee_table


anim coffee_table_anim
    args type args;
with
    double_buffer is on;
    lookat = <0 0 3>;
    args check;
is
    shape scene is
        coffee_table with
            material is wood;
        end;
        plane with
            magnify by 10;
            render_mode is wireframe;
        end;
    end;    // scene

    mouse_controlled_shape scene;
end;    // coffee_table_anim
