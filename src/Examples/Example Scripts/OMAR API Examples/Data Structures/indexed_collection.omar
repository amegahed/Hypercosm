{***************************************************************}
{ |\  /|                                                We Put  }
{ | >< Hypercosm      indexed_collection.omar           3d      }
{ |/  \|                                                To Work }
{***************************************************************}
{                                                               }
{       This is a simple example of how to use Hypercosm's      }
{       collection utilities.                                   }
{                                                               }
{***************************************************************}
{                Copyright (c) 2006 Hypercosm, LLC.             }
{***************************************************************}
do example;

include "system/printing.ores";
include "system/string_conversions.ores";
include "system/indexed_collection.ores";
include "system/linked_list.ores";

subject thing
extends
	printable printing object
does
	// constructor
	//
	verb new with
		integer i = 0;
	end;    // new

	// converting method
	//
	string type question to_string;

	// printing method
	//
	verb print;
has
	// attributes
	//
	integer i;
is
	//
	// constructor
	//

	verb new with
		integer i = 0;
	is
		thing's i = i;
	end;    // new

	//
	// converting method
	//

	string type question to_string is
		answer integer_to_string of i;
	end;    // to_string

	//
	// printing method
	//

	verb print is
		write "i = ", to_string, ;
	end;    // print
end;    // thing

verb example is
	thing type thing1 with
		i = 5;
	end;
	thing type thing2 with
		i = 10;
	end;
	thing type thing3 with
		i = 15;
	end;
	thing type thing4 with
		i = 20;
	end;
	mutable_collection type collection is new linked_list;
	indexed_collection type indexed_collection indexing collection;
	thing type thing is none;
	
	collection add thing2;
	collection add thing1;
	collection add thing3;
	collection add thing2;
	collection add thing4;
	collection add thing2;
	
	write "collection of ", collection get_size, " items :", ;
	collection print;
	write;
	write "element #2:", ;
	print indexed_collection get_element 2;
end;    // example