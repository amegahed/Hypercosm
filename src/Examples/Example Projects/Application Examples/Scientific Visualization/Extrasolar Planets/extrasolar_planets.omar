do extrasolar_planet_anim;

include "system/3d.ores";
include "system/anims.ores";
include "system/text_resource.ores";
include "system/string_utils.ores";
include "system/native_overlay_text.ores";
include "system/beveled_slider.ores";
include "system/view_alignments.ores";
include "system/common_materials.ores";
include "system/label_text.ores";
include "planet_parser.ores";

// time and scale exaggeration factors
//
scalar planet_scale = 50;       // exaggeration of planet sizes
scalar time_scale = 5;          // number of simualted days that pass each second

// user interface
//
beveled_slider type planet_scale_control controls planet_scale from 1 to 100 with
    end1 = <-1 0 0>;
    end2 = <1 0 0>;
    radius = .0125;
    knob_size = .05;
    bevel_width = .025;
    knob_material is metal;
    bar_material is plastic colored red;
end;
beveled_slider type time_scale_control controls time_scale from .1 to 10 with
    end1 = <-1 0 0>;
    end2 = <1 0 0>;
    radius = .0125;
    knob_size = .05;
    bevel_width = .025;
    knob_material is metal;
    bar_material is plastic colored blue;
end;
boolean labels_visible is true;

// solar planets
//
planet type jupiter named "Jupiter" with
    mass = 1;
    color = red;
    material is chalk poster "jupiter_map.jpg";
    major_axis = 5.2;
    period = 11.9 * 365.25;
end;
planet type earth named "Earth" with
    mass = (earth_radius / jupiter_radius) ^ 3;
    color = blue;
    //material is chalk poster "earth_map.jpg";
    material is plastic colored light blue;
    major_axis = 1;
    period = 365.25;
end;

anim extrasolar_planet_anim 
    args type args;
with
    eye = <-2.75 0 1>;
    background = black;
    double_buffer is on;
    show_controls is false;
    auto_upright is true;
    auto_pivot is false;
    args check;
is
    text_resource type text_resource named "planet_table.txt";
    string type title is "The Known Extrasolar Planets as of January 2002";
    string type subtitle is "The Red Orbit represents Jupiter and the Blue Orbit represents Earth for comparison";
    planet type planets[];
    picture scene is
        
        // environment
        //
        default_lights with
            interaction get_trans trans;
        end;
        //default_background at 9;
        
        // planet size exaggeration slider
        //
        planet_scale_control instance with
            magnify by .1;
            place_on_view_ray at <0 -.825 0> distance .25;
        end;
        
        // planet time exaggeration slider
        //
        time_scale_control instance with
            magnify by .1;
            place_on_view_ray at <0 -.65 0> distance .25;
        end;
    
        // planets in orbit
        //
        for each planet type planet in planets do
            planet instance with
                magnify by planet_scale about planet get_location;
            end;
            planet orbit;
        end;
        earth instance with
            magnify by planet_scale about earth get_location;
        end;
        earth orbit;
        jupiter instance with
            magnify by planet_scale about jupiter get_location;
        end;
        jupiter orbit;
    end;    // scene  
    
	verb check_mouse is
		simulation type current_simulation is none;
		mouse_interaction type current_interaction is none;
		current_simulation is (global simulation);
		current_interaction is mouse_interaction type (global interaction);
		
        if planet_scale_control's mouse_is_over or time_scale_control's mouse_is_over then
        	if some interaction then 
            	(mouse_interaction type interaction) override;
            end;
        end;
	end;	// check_mouse
	
	verb check_keys is
		integer key = get_key;
		if key = char_to_key of "l" then
			labels_visible is not labels_visible;
		end;
	end;	// check_keys
	
	verb draw_titles is
		overlay_text title at <0 .9 0> with
			size = get_default_text_size * 2;
		end;
		overlay_text subtitle at <0 .8 0> with
			size = get_default_text_size;
		end;
	end;	// draw_titles
	
	verb draw_legends is
        string type legend;
        legend is "Planet Size Exaggeration Factor = ";
        legend add integer_to_string of trunc planet_scale;
        overlay_text legend at <0 -.9 0> with
            size = width div 50; 
        end;
        legend is "Simulated Days / Second = ";
        legend add (scalar_to_string of time_scale to 2);
        overlay_text legend at <0 -.725 0> with
            size = width div 50;
        end;
	end;    // draw_legends
	
	verb draw_labels is
	    if labels_visible then
    	    for each planet type planet in planets do
    	        planet label;
    	    end;
    	    earth label;
    	    jupiter label;
	    end;
	end;    // draw_labels
    verb update_planets is
    
        // update extrasolar planets
        //
        for each planet type planet in planets do
            planet set_speed to time_scale;
            planet update;
        end;
        
        // update earth
        //
        earth set_speed to time_scale;
        earth update;
        
        // update jupiter
        //  
        jupiter set_speed to time_scale;
        jupiter update;
    end;    // update_planets
    verb update_cursor is
        if planet_scale_control's mouse_is_over or time_scale_control's mouse_is_over then
            set_cursor to crosshair;
        else
            set_cursor to arrow;
        end;
    end;    // update_cursor
        	
	verb update is
		check_mouse;
		check_keys;
		draw_titles;
		draw_legends;
		draw_labels;
		update_planets;
		update_cursor;
	end;	// update
	    
    // wait for data to download
    //
    text_resource finish_loading;
    
    // parse planets
    //
    planets is parse_planets from text_resource get_lines;
    // run!
    //
    set_manual_near_far_planes on from .01 to 100;   
    mouse_controlled_picture scene doing update;
        //titled title subtitled subtitle;
end;    // extrasolar_planet_anim
