{***************************************************************}
{ |\  /|                                                We Put  }
{ | >< Hypercosm              queen.omar                3d      }
{ |/  \|                                                To Work }
{***************************************************************}
{                                                               }
{       Contains a definition of the shape of a chess piece.    }
{                                                               }
{***************************************************************}
{                Copyright (c) 2006 Hypercosm, LLC.             }
{***************************************************************}
do queen_anim;

include "system/3d.ores";
include "system/anims.ores";

shape queen is
	shape crown with
		integer steps = 8;
	is
		for integer counter = 0 .. steps - 1 do
			scalar angle = counter / steps * 360;
			triangle <0 1 0> <0 -1 0> <2 0 5> with
				move to <3 0 0>;
				rotate by angle around <0 0 1>;
			end;
		end;
	end;
	
	color = white;
	cylinder with
		end1 = <0 0 0>;
		end2 = <0 0 1>;
		radius = 8;
	end;
	disk with
		radius = 8;
	end;
	hyperboloid1 with
		end1 = <0 0 1>;
		end2 = <0 0 5>;
		radius1 = 8;
		radius2 = 5;
	end;
	sphere with
		radius = 6.5;
		scale by .3 along <0 0 1>;
		move to <0 0 5>;
	end;
	hyperboloid1 with
		end1 = <0 0 5>;
		end2 = <0 0 27>;
		radius1 = 5;
		radius2 = 1.75;
	end;
	sphere with
		radius = 4;
		scale by .2 along <0 0 1>;
		move to <0 0 27>;
	end;
	sphere with
		radius = 2.5;
		scale by 2 along <0 0 1>;
		move to <0 0 30>;
	end;
	sphere with
		radius = 1;
		move to <0 0 35>;
	end;
	crown with
		move to <0 0 27.5>;
	end;
end;    // queen

anim queen_anim
	args type args;
with
	eye = <20 -60 40>;
	lookat = <0 0 17.5>;
	double_buffer is on;
	args check;
is
	mouse_controlled_shape queen;
end;    // queen_anim
