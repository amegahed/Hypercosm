{***************************************************************}
{                          point.omar                           }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}
do point_light_anim;

include "system/3d.ores";
include "system/anims.ores";
include "system/shapes.ores";
include "system/arrow_shapes.ores";
include "system/square_grid.ores";
include "system/common_materials.ores";
include "system/futura_l.ores";

shape point_light_scene with
	scalar time = 0;
is
	scalar x = sin (time * 90);
	
	point_light with
		move to <x 0 .9>;
		brightness = 1.5;
	end;
	mark at <x 0 1>;
	futura_l_word "POINT LIGHT" with
		rotate by 90 around <1 0 0>;
		move to <x 0 1.1>;
	end;
	square_grid with
		steps = 16;
		material is wood;
		magnify by 1.25;
	end;
	sphere with
		material is plastic colored red;
		center = <0 0 .25>;
		radius = .25;
	end;
end;    // point_light_scene
	
	
anim point_light_anim with
	eye = <2 -8 4>;
	field_of_view = 30;
	lookat = <0 0 .25>;
	render_mode is shaded;
	shading is vertex;
	facets = 16;
is
	mouse_controlled_animated_shape point_light_scene lit by none;
end;    // point_light_anim
