do parse_and_display_xml;

include "system/3d.ores";
include "system/anims.ores";
include "system/xml_resource.ores";
include "system/native_xml_reader.ores";
include "group_parser.ores";
include "scene_parser.ores";

anim parse_and_display_xml 
	args type args;
is
	xml_resource type xml_resource named "scene.xml";
	string type xml is xml_resource get_xml;
	xml_reading type xml_reader is new xml_reader of xml;
	object_group type scene is none;
	
	// parse start of document
	//
	xml_reader require start_document;
	xml_reader skip_element;
	xml_reader require start_element named "root";
	xml_reader skip_element;
	// parse groups
	//
	while xml_reader is_ok and xml_reader is_next start_element named "group" do
		string type id;
		object_group type group;	
		group is parse_group named id from xml_reader;
		group_library add_group group named id;
	end;
		
	// parse scene
	//
	scene is parse_scene from xml_reader;
	
	// parse end of document
	//
	xml_reader require end_element named "root";
	xml_reader skip_element;
	xml_reader require end_document;
	xml_reader skip_element;
	xml_reader close;
	
	// show scene
	//
	mouse_controlled_actor scene;
end;	// parse_and_display_xml
