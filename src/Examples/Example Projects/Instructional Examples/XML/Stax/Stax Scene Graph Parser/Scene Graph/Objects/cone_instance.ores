{***************************************************************}
{ |\  /|                                                We Put  }
{ | >< Hypercosm            cone_instance.ores          3d      }
{ |/  \|                                                To Work }
{***************************************************************}
{                                                               }
{       Contains a definition of a cone instance in the         }
{       scene graph.                                            }
{                                                               }
{***************************************************************}
{                Copyright (c) 2008 Hypercosm, LLC.             }
{***************************************************************}

include "object_instance.ores";

subject cone_instance
extends 
	object_instance
does
	// constructor
	//
	verb new
		named string type name is none;
	with
		vector end1 = <0 0 1>;
		vector end2 = <0 0 -1>;
		scalar radius1 = 0;
		scalar radius2 = 1;
		trans type trans = unit_trans;
		material type material is none;
	end;
	
	// rendering methods
	//
	shape instance;
	
	// copying and cloning methods
	//    
	verb copy 
		from cone_instance type source;
	end;
	cone_instance type question clone;
	
	// printable methods
	//
	string type question to_string;
has
	// attributes 
	//
	vector end1, end2;
	scalar radius1, radius2;
is
	//
	// constructor
	//
	
	
	verb new
		named string type name is none;
	with
		vector end1 = <0 0 1>;
		vector end2 = <0 0 -1>;
		scalar radius1 = 0;
		scalar radius2 = 1;
		trans type trans = unit_trans;
		material type material is none;
	is
		// call superclass constructor
		//
		object_instance new named name with
			trans = static trans;
			material is static material;
		end;
		
		// set attributes
		//
		with cone_instance do
			its end1 = end1;
			its end2 = end2;
			its radius1 = radius1;
			its radius2 = radius2;
		end;
	end;    // new
	
	//    
	// rendering methods
	//
	
	
	shape instance is
		global cone with
			end1 = static end1;
			end2 = static end2;
			radius1 = static radius1;
			radius2 = static radius2;
			global trans = trans;
			global material is material;
		end;
	end;    // instance
	
	
	//   
	// copying and cloning methods
	// 
	
	  
	verb copy 
		from cone_instance type source;
	is
		// call superclass method
		//
		object_instance static copy from source;
			
		// copy attributes of source
		//
		end1 = source's end1;
		end2 = source's end2;
		radius1 = source's radius1;
		radius2 = source's radius2;
	end;    // copy
	
	
	cone_instance type question clone is
		cone_instance type cone2;
		cone2 copy from cone_instance;
		answer cone2;
	end;    // clone
	
	//
	// printable methods
	//
	
	
	string type question to_string is
		answer "cone instance";
	end;    // to_string
end;    // cone_instance

