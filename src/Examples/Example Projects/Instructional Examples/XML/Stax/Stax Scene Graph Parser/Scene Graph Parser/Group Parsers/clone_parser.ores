{***************************************************************}
{ |\  /|                                                We Put  }
{ | >< Hypercosm          clone_parser.ores             3d      }
{ |/  \|                                                To Work }
{***************************************************************}
{                                                               }
{       This file contains a parser for parsing object          }
{       clones from an XML stream.                              }
{                                                               }
{***************************************************************}
{                Copyright (c) 2007 Hypercosm, LLC.             }
{***************************************************************}

include "system/xml_reading.ores";
include "object_instance.ores";
include "group_library.ores";
include "trans_parser.ores";
include "material_parser.ores";

object_instance type question parse_clone
	from xml_reading type xml_reader;
is
	// 3ds instance attributes
	//
	string type group_name;
	trans type trans;
	material type material is none;
	
	// parse start element
	//          
	xml_reader require start_element named "clone";
	
	// parse attributes
	//
	for integer counter = 1 .. xml_reader get_attribute_count do
		string type name is xml_reader get_attribute_name of counter;
		string type value is xml_reader get_attribute_value of counter;
		
		if name matches "group" then
			group_name = value;
		end;
	end;
	xml_reader skip_element;
	
	// parse nested elements
	//
	while xml_reader is_ok and not xml_reader is_next end_element named "clone" do
		// parse transformation
		//      
		if xml_reader is_next start_element named "trans" then
			parse_trans trans from xml_reader;
			
		// parse material
		//
		elseif xml_reader is_next start_element named "material" then
			material is parse_material from xml_reader;
			
		// skip unknown elements
		//
		else
			xml_reader skip_element;    
		end;
	end;
	// parse end element
	//          
	xml_reader require end_element named "clone";
	xml_reader skip_element;
	// create and return clone
	//  
	answer (new group_instance
		of (group_library get_group named group_name)
	with
		trans = static trans;
		material is static material;
	);
end;    // parse_clone
