{***************************************************************}
{                          skew.omar                            }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}
do skew_anim;

include "system/3d.ores";
include "system/anims.ores";
include "system/arrow_shapes.ores";
include "system/futura_l.ores";
include "system/snowman.omar";

shape skew_scene with
	scalar time = 0;
is
	static material type material1 is glass colored white;
	static material type material2 is glass colored red;
	scalar h = 3.6 * .5;
	scalar x = cos (time * 90);
	snowman with
		magnify by .5;
		material is material1;
		edges is silhouette;
		render_mode is shaded_line;
	end;
	
	snowman with
		magnify by .5;
		material is material2;
		edges is silhouette;
		render_mode is shaded_line;
		
		// apply the "skew" transformation
		//
		skew from <0 0 h> to <x 0 h>;
	end;
	
	// annotations
	//
	mark at <0 0 0>;
	arrow from <0 0 h> to <x 0 h>;
	arrow from <0 0 0> to <0 0 h>;
	arrow from <0 0 0> to <x 0 h>;
	overlay_text "skew from <vector point1> to <vector point2> [about <vector point>];" at <0 .85 0>;
	
	{
	futura_l_word "SKEW" with
		rotate by 90 around <1 0 0>;
		move to <.75 0 h>;
	end;
	}
end;    // skew_scene
	
	
anim skew_anim
	args type args;
with
	eye = <2 -8 4>;
	field_of_view = 30;
	lookat = <0 0 .85>;
	args check;
is
	mouse_controlled_animated_shape skew_scene;
end;    // skew_anim
