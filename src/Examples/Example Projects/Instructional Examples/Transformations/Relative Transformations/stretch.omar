{***************************************************************}
{                          stretch.omar                         }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}
do stretch_anim;

include "system/3d.ores";
include "system/anims.ores";
include "system/arrow_shapes.ores";
include "system/futura_l.ores";
include "system/snowman.omar";

shape stretch_scene with
	scalar time = 0;
is
	static material type material1 is glass colored white;
	static material type material2 is glass colored red;
	scalar h1 = 3.6 * .5;
	scalar stretch_factor = .75 + sin (time * 90) * .25;
	scalar h2 = h1 * stretch_factor;
	
	snowman with
		magnify by .5;
		material is material1;
		edges is silhouette;
		render_mode is shaded_line;
	end;
	
	snowman with
		magnify by .5;
		material is material2;
		edges is silhouette;
		render_mode is shaded_line;
		
		// apply the "stretch" transformation
		//
		stretch by stretch_factor along <0 0 1>;
	end;
	
	// annotations
	//
	mark at <0 0 0>;
	line [<0 0 h1> <.8 0 h1>];
	line [<0 0 h2> <.6 0 h2>];
	arrow from <0 0 0> to <0 0 h1>;
	double_arrow from <.6 0 0> to <.6 0 h2>;
	double_arrow from <.8 0 0> to <.8 0 h1>;
	overlay_text "stretch by <scalar factor> along <vector axis> [about vector point];" at <0 .85 0>;
	
	{
	futura_l_word "STRETCH" with
		rotate by 90 around <1 0 0>;
		move to <-.85 0 1>;
	end;
	}
end;    // stretch_scene
	
anim stretch_anim
	args type args;
with
	eye = <2 -8 4>;
	field_of_view = 30;
	lookat = <0 0 .85>;
	args check;
is
	mouse_controlled_animated_shape stretch_scene;
end;    // stretch_anim
