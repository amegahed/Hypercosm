{***************************************************************}
{                          orient.omar                          }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}
do orient_anim;

include "system/3d.ores";
include "system/anims.ores";
include "system/arrow_shapes.ores";
include "system/futura_l.ores";
include "system/snowman.omar";

shape orient_scene with
	scalar time = 0;
is
	static material type material1 is glass colored white;
	static material type material2 is glass colored red;
	scalar x = cos (time * 90);
	scalar h = 3.6 * .5;
	snowman with
		magnify by .5;
		material is material1;
		edges is silhouette;
		render_mode is shaded_line;
	end;
	
	snowman with
		magnify by .5;
		material is material2;
		edges is silhouette;
		render_mode is shaded_line;
		rotate by 180 around <0 0 1>;
				
		// apply the "orient" transformation
		//
		orient from <0 0 h> to <x 0 h>;
	end;
	
	// annotations
	//
	mark at <0 0 0>;
	arrow from <0 0 0> to <0 0 h>;
	arrow from <0 0 0> to <x 0 h>;
	line [<0 0 h> <x 0 h>];
	overlay_text "orient from <vector v1> to <vector v2> [about <vector point>];" at <0 .85 0>;
	
	{
	futura_l_word "ORIENT" with
		rotate by 90 around <1 0 0>;
		move to <.75 0 1.8>;
	end;
	}
end;    // orient_scene
	
	
anim orient_anim
	args type args;
with
	eye = <2 -8 4>;
	field_of_view = 30;
	lookat = <0 0 .85>;
	args check;
is
	mouse_controlled_animated_shape orient_scene;
end;    // orient_anim
