{***************************************************************}
{                          shaded_triangle.omar                 }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}
do shaded_triangle_anim;

include "system/3d.ores";
include "system/anims.ores";
include "system/futura_l.ores";
include "system/arrow_shapes.ores";

shape labelled_shaded_triangle is
	shape shaded_triangle_primitive is
		shaded_triangle <-.75 -.75 0> <-.5 1 0> <1 -1.25
			0> <-.25 -.25 .5> <-.25 .5 .5> <.25 -.25 .5>
		with
			move to <0 0 -.01>;
		end;
	end;
	
	shaded_triangle_primitive with
		material is glass colored shaded_triangle_color;
		render_mode is shaded_line;
	end;
	// vertices
	//
	mark at <-.75 -.75 0>;
	mark at <-.5 1 0>;
	mark at <1 -1.25 0>;
	// normals
	//
	arrow from <-.75 -.75 0> to <-1 -1 .5>;
	arrow from <-.5 1 0> to <-.75 1.25 .5>;
	arrow from <1 -1.25 0> to <1.25 -1.5 .5>;
	// vertex labels
	//
	futura_l_word "VERTEX1" with
		move to <-.75 -1 0>;
	end;
	futura_l_word "VERTEX2" with
		move to <-.5 1.25 0>;
	end;
	futura_l_word "VERTEX3" with
		move to <1 -1.5 0>;
	end;
	// normal labels
	//
	futura_l_word "NORMAL1" with
		rotate by 90 around <1 0 0>;
		move to <0 0 .1>;
		direct from <0 0 1> to <-.25 -.25 .5>;
		move to <-1 -1 .5>;
	end;
	futura_l_word "NORMAL2" with
		rotate by 90 around <1 0 0>;
		move to <0 0 .1>;
		direct from <0 0 1> to <-.25 .5 .5>;
		move to <-.75 1.25 .5>;
	end;
	futura_l_word "NORMAL3" with
		rotate by 90 around <1 0 0>;
		move to <0 0 .1>;
		direct from <0 0 1> to <.25 -.25 .5>;
		move to <1.25 -1.5 .5>;
	end;
end;    // labelled_shaded_triangle

shape shaded_triangle_scene is
	labelled_shaded_triangle;
	axes;
end;    // shaded_triangle_scene
	
	
anim shaded_triangle_anim
	args type args;
with
	eye = <2 -8 4>;
	field_of_view = 32;
	window_name is "The Shaded Triangle Primitive";
	args check;
is
	mouse_controlled_shape shaded_triangle_scene;
end;    // shaded_triangle_anim
