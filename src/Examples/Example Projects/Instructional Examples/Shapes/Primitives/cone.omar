{***************************************************************}
{                          cone.omar                            }
{          Object-Oriented Modelling and Rendering (OMAR)       }
{***************************************************************}
{                                                               }
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}
do cone_anim;

include "system/3d.ores";
include "system/anims.ores";
include "system/futura_l.ores";
include "system/arrow_shapes.ores";

shape labelled_cone with
	scalar umin = -30;
	scalar umax = 260;
is
	shape cone_primitive is
		cone with
			radius1 = .5;
			radius2 = 1.0;
			umin = static umin;
			umax = static umax;
		end;
	end;
	
	cone_primitive with
		material is glass colored cone_color;
		render_mode is shaded_line;
	end;
	
	futura_l_word "UMIN" with
		move to <1.25 0 0>;
		rotate by umin around <0 0 1>;
	end;
	futura_l_word "UMAX" with
		move to <1.25 0 0>;
		rotate by umax around <0 0 1>;
	end;
	mark at <0 0 -1>;
	mark at <0 0 1>;
	futura_l_word "END1" with
		move to <0 0 -1.1>;
	end;
	futura_l_word "END2" with
		move to <0 0 1.1>;
	end;
	double_arrow from <0 0 -1> to <1 0 -1> with
		rotate by 70 around <0 0 1>;
	end;
	futura_l_word "RADIUS1" with
		move to <.5 -.1 -1>;
		rotate by 70 around <0 0 1>;
	end;
	double_arrow from <0 0 1> to <.5 0 1> with
		rotate by 220 around <0 0 1>;
	end;
	futura_l_word "RADIUS2" with
		rotate by 180 around <1 0 0>;
		rotate by 180 around <0 1 0>;
		move to <.9 0 1>;
		rotate by 220 around <0 0 1>;
	end;
	// arcs showing longitude
	//
	arrow_arc to umax with
		radius = .25;
	end;
	arrow_arc to umin with
		radius = .4;
	end;
	line [<0 0 0> <1 0 0>] with
		rotate by umin around <0 0 1>;
	end;
	line [<0 0 0> <1 0 0>] with
		rotate by umax around <0 0 1>;
	end;
end;    // labelled_cone

shape cone_scene is
	labelled_cone;
	axes;
end;    // cone_scene

anim cone_anim
	args type args;
with
	eye = <2 -8 4>;
	field_of_view = 32;
	window_name is "The Cone Primitive";
	args check;
is
	mouse_controlled_shape cone_scene;
end;    // cone_anim
