do translated_3ds_prism_anim;

include "system/3d.ores";
include "system/3ds_prism.ores";
include "system/3ds_texture_mapping.ores";
include "system/anims.ores";
include "system/arrow_shapes.ores";

shape translated_3ds_prism_scene with
	scalar time = 0;
	scalar tessellation = 1;
is
	static material type hyper_material is plastic poster "hyper.jpg";
	static material type tie_dye_material is plastic poster "tie_dye.jpg";
	static material type flag_material is plastic poster "flag.gif";
		
	translated_3ds_prism with		
	    side1_length = 50;
	    side2_length = 40;
	    side3_length = 30;		    
	    height = 50;
		side1_segments = round (1 * tessellation);
		side2_segments = round (2 * tessellation);
		side3_segments = round (3 * tessellation);
		height_segments = round (4 * tessellation);
		gen_mapping_coords is true;
        // apply animated texture mapping
        //
		translated_3ds_planar_mapping with
			length = 50; width = 70;
			u_tile = 1; v_tile = 1; w_tile = 1;
			u_flip is false; v_flip is false; w_flip is false;
			alignment is y;
			with trans do
				its origin = <35 35 25>;
				its x_axis = <1 0 0>;
				its y_axis = <0 1 0>;
				its z_axis = <0 0 1>;
			end;
		    rotate the trans by time * 45 around <0 1 0> about trans's origin;
		end;
		
	    material is hyper_material; 
	    move to <-25 -20 0>;
	end;
end;	// translated_3ds_prism_scene
	
	
anim translated_3ds_prism_anim with
    background_name is none;
    background = light grey;
is
	shape environment is
		plane with
			magnify by 50;
			render_mode is wireframe;
		end;
	end;    // environment
	
	mouse_controlled_animated_tessellated_shape translated_3ds_prism_scene
		within new static_shape of environment 
	    titled "3DS Prism"
	with
		auto_camera is on;
	end;
end;	// translated_3ds_prism_anim
