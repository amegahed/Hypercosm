do translated_3ds_box_anim;

include "system/3d.ores";
include "system/3ds_box.ores";
include "system/3ds_texture_mapping.ores";
include "system/anims.ores";
include "system/arrow_shapes.ores";

shape translated_3ds_box_scene with
	scalar time = 0;
	scalar tessellation = 1;
is
	static material type hyper_material is plastic poster "hyper.jpg";
	static material type tie_dye_material is plastic poster "tie_dye.jpg";
	static material type flag_material is plastic poster "flag.gif";
	
	translated_3ds_box with
	    length = 25;
	    width = 25;
	    height = 25;
	    length_segments = round (3 * tessellation);
	    width_segments = round (2 * tessellation);
	    height_segments = round (1 * tessellation);
        // apply animated texture mapping
        //
		translated_3ds_planar_mapping with
			length = 25; width = 25;
			u_tile = 1; v_tile = 1; w_tile = 1;
			u_flip is false; v_flip is false; w_flip is false;
			alignment is y;
			with trans do
				its origin = <0 0 12.5>;
				its x_axis = <1 0 0>;
				its y_axis = <0 1 0>;
				its z_axis = <0 0 1>;
			end;
		    rotate the trans by time * 45 around <0 1 0> about trans's origin;
		end;
		
		move to <25 0 0>;
	    material is hyper_material; 
	end;
	
	translated_3ds_box with
	    length = 15;
	    width = 25;
	    height = 10;
	    length_segments = round (1 * tessellation);
	    width_segments = round (1 * tessellation);
	    height_segments = round (1 * tessellation);
	    gen_mapping_coords is true;
	    
	    // apply animated texture mapping
	    //
		translated_3ds_planar_mapping with
			length = 15; width = 25;
			u_tile = 1; v_tile = 1; w_tile = 1;
			u_flip is false; v_flip is false; w_flip is false;
			alignment is z;
			with trans do
				its origin = <0 0 5>;	
				its x_axis = <1 0 0>;
				its y_axis = <0 1 0>;
				its z_axis = <0 0 1>;
			end;
		    rotate the trans by time * 45 around <0 0 1> about trans's origin;
		end;
	    move to <-30 0 0>;
	    material is flag_material;	    
	end;
	
	translated_3ds_box with
	    length = 25;
	    width = 15;
	    height = 10;
	    length_segments = round (1 * tessellation);
	    width_segments = round (1 * tessellation);
	    height_segments = round (1 * tessellation);
	    gen_mapping_coords is true;
	    move to <0 -30 0>;
	    material is flag_material;
	end;
end;	// translated_3ds_box_scene
	
	
anim translated_3ds_box_anim with
    background_name is none;
    background = light grey;
is	
	shape environment is
		plane with
			magnify by 50;
			render_mode is wireframe;
		end;
	end;    // environment
	
	mouse_controlled_animated_tessellated_shape translated_3ds_box_scene
		within new static_shape of environment 
	    titled "3DS Box"
	with
		auto_camera is on;
	end;
end;	// translated_3ds_box_anim
