do translated_3ds_extruded_line_anim;

include "system/3d.ores";
include "system/3ds_line.ores";
include "system/3ds_extrude.ores";
include "system/anims.ores";
include "system/arrow_shapes.ores";

shape closed_loop with
    scalar time = 0;
	scalar tessellation = 1;
is
	translated_3ds_line of [
		translated_3ds_connected_cyclic_spline through [
			translated_3ds_corner_vertex at <-25 -25 0>	
			translated_3ds_corner_vertex at <25 -25 0>
			translated_3ds_corner_vertex at <25 25 0>
			translated_3ds_corner_vertex at <-25 25 0>
			]				
	] with
	    steps = round (4 * tessellation);
	    
	    translated_3ds_extrude with
            amount = cos (time * 90) * 15;
            segments = round (2 * tessellation);
            gen_mapping_coords is true;
	    end;
	end;
end;	// closed_loop

shape backwards_loop with
    scalar time = 0;
    scalar tessellation = 1;
is
	translated_3ds_line of [
		translated_3ds_connected_cyclic_spline through [
			translated_3ds_corner_vertex at <-25 -25 0>	
			translated_3ds_corner_vertex at <-25 25 0>
			translated_3ds_corner_vertex at <25 25 0>
			translated_3ds_corner_vertex at <25 -25 0>
			]				
	] with
	    steps = round (4 * tessellation);
	    
	    modifier_stack push (new translated_3ds_extrude with
            amount = cos (time * 90) * 15;
            segments = round (2 * tessellation);
            gen_mapping_coords is true;
	    );
	end;
end;	// backwards_loop

shape curvy_loop with
    scalar time = 0;
    scalar tessellation = 1;
is
	translated_3ds_line of [
		translated_3ds_connected_cyclic_spline through [
			translated_3ds_bezier_vertex at <0 -25 0> from -<50 0 0> to <50 0 0>
			translated_3ds_corner_vertex at <25 0 0>
			translated_3ds_bezier_vertex at <0 25 0> from -<-50 0 0> to <-50 0 0>
			translated_3ds_bezier_corner_vertex at <-25 0 0> from -<0 -50 0> to <-50 -50 0>
			]
		{
		translated_3ds_connected_spline through [
			translated_3ds_corner_vertex at <-10 -10 0>
			translated_3ds_corner_vertex at <10 -10 0>
			translated_3ds_corner_vertex at <10 10 0>
			translated_3ds_corner_vertex at <-10 10 0>
			translated_3ds_corner_vertex at <-10 -10 0>
			]
		}
		]
	with
	    steps = round (8 * tessellation);
	    
	    modifier_stack push (new translated_3ds_extrude with
            amount = cos (time * 90) * 15;
            segments = round (3 * tessellation);
            cap_start is true;
            cap_end is true;
            capping_kind is morph;
            output_kind is mesh;
            gen_mapping_coords is true;
            gen_mat_IDs is true;
            use_shape_IDs is false;
            smooth is true;
    		active is true;
	    );
	end;
end;	// curvy_loop

shape translated_3ds_extruded_line_scene with 
    scalar time = 0;
	scalar tessellation = 1;
is
    static material type hyper_material is plastic poster "hyper.jpg";
    
	curvy_loop with 
	    time = static time;
	    tessellation = static tessellation;
        material is hyper_material;
	end;
end;    // translated_3ds_extruded_line_scene
	
	
anim translated_3ds_extruded_line_anim with
    background_name is none;
    background = light grey;
is
	shape environment is
		plane with
			magnify by 50;
			render_mode is wireframe;
		end;
	end;    // environment
	
	mouse_controlled_animated_tessellated_shape translated_3ds_extruded_line_scene
		within new static_shape of environment
	    titled "3DS Extruded Line"
	with
		auto_camera is on;
	end;
end;	// translated_3ds_extruded_line_anim
