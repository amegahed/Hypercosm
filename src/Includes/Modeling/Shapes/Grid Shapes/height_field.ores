{***************************************************************}
{ |\  /|                                                We Put  }
{ | >< Hypercosm           height_field.ores            3d      }
{ |/  \|                                                To Work }
{***************************************************************}
{                                                               }
{       Contains a definition of shape in the form of a         }
{       height field.                                           }
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}

include "system/grid_mesh.ores";

shape height_field
	scalar height[, ];
with
	// tessellation parameters
	//
	boolean triangulate is false;
	boolean alternate is true;
	
	// rendering parameters
	//
	boolean smoothing is true;
	boolean one_sided is false;
	boolean reversed is false;
		
	// texturing parameters
	//
	boolean textured is true;
	texture_mapping type mapping is unit_parametric_mapping;   
is
	vector_grid type vertex_grid;
	integer xmin = min height[], xmax = max height[];
	integer ymin = min height, ymax = max height;

	// create vertex grid
	//
	dim vertex_grid[ymin..ymax, xmin..xmax];
	for integer counter1 = ymin..ymax do
		scalar y = (counter1 - ymin) / (ymax - ymin) * 2 - 1;
		for integer counter2 = xmin..xmax do
			scalar x = (counter2 - xmin) / (xmax - xmin) * 2 - 1;
			scalar z = height[counter1, counter2];
			vertex_grid[counter1, counter2] = <x y z>;
		end;
	end;
	
	// create mesh from grid of vertices
	//
	grid_mesh of vertex_grid with
	
		// tessellation parameters
		//
		triangulate is static triangulate;
		alternate is static alternate;
		
		// rendering paramters
		//
		smoothing is static smoothing;
		closed is static one_sided;
		reversed is static reversed; 
			   
		// texturing parameters
		//
		textured is static textured;
		mapping is static mapping;
	end;
end;    // height_field