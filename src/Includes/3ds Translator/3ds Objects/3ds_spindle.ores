{***************************************************************}
{ |\  /|                                                We Put  }
{ | >< Hypercosm          3ds_spindle.ores              3d      }
{ |/  \|                                                To Work }
{***************************************************************}
{                                                               }
{       Contains spindle definitions that correspond to         }
{       entities from Discreet's 3D Studio Max software.        }
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}

include "system/3ds_sliceable_object.ores";
include "system/3ds_modifier_stacks.ores";
include "system/3ds_cylinder.ores";
include "system/3ds_cone.ores";

final subject translated_3ds_spindle
extends
	translated_3ds_collapsable translated_3ds_sliceable_object
does
	// constructor
	//
	verb new with
	
		// geometry parameters
		//
		scalar radius = 1;
		scalar height = 2;
		scalar cap_height = .5;
		scalar blend = 0;
			
		// tessellation parameters
		//
		integer sides = 16;
		integer cap_segments = 1;
		integer height_segments = 1;
		boolean smooth is true;
		
		// slicing parameters
		//
		boolean slice_on is false;
		scalar slice_from = 0;
		scalar slice_to = 0;
		
		// texturing parameters
		//
		boolean gen_mapping_coords is false;
			
		// additional parameters that
		// are not provided by 3DS Max
		//
		boolean capping is true;
	end;
	
	// setting method
	//
	verb set with
	
		// geometry parameters
		//
		scalar radius = 1;
		scalar height = 2;
		scalar cap_height = .5;
		scalar blend = 0;
			
		// tessellation parameters
		//
		integer sides = 16;
		integer cap_segments = 1;
		integer height_segments = 1;
		boolean smooth is true;
		
		// slicing parameters
		//
		boolean slice_on is false;
		scalar slice_from = 0;
		scalar slice_to = 0;
		
		// texturing parameters
		//
		boolean gen_mapping_coords is false;
			
		// additional parameters that
		// are not provided by 3DS Max
		//
		boolean capping is true;
	end;
		
	// collapsing method
	//
	translated_3ds_stackable type question get_collapsed;
	
	// recycling methods
	//
	objective translated_3ds_spindle type question get;
	reference verb recycle; 
has
	// attributes
	//
	scalar radius;
	scalar height;
	scalar cap_height;
	scalar blend;
		
	// tessellation attributes
	//
	integer sides;
	integer cap_segments;
	integer height_segments;
	boolean smooth;
		
	// additional attributes that
	// are not provided by 3DS Max
	//
	boolean capping;
	
private 
	// recycling attributes
	//
	translated_3ds_spindle type next is none;
is
	// recycling free list
	//
	translated_3ds_spindle type free_list is none;
	
	//
	// constructor
	//
	
	verb new with
	
		// geometry parameters
		//
		scalar radius = 1;
		scalar height = 2;
		scalar cap_height = .5;
		scalar blend = 0;
			
		// tessellation parameters
		//
		integer sides = 16;
		integer cap_segments = 1;
		integer height_segments = 1;
		boolean smooth is true;
		
		// slicing parameters
		//
		boolean slice_on is false;
		scalar slice_from = 0;
		scalar slice_to = 0;
		
		// texturing parameters
		//
		boolean gen_mapping_coords is false;
			
		// additional parameters that
		// are not provided by 3DS Max
		//
		boolean capping is true;
	is
		translated_3ds_sliceable_object new with
			
			// set slicing parameters
			//
			slice_on is static slice_on;
			slice_from = static slice_from;
			slice_to = static slice_to;
					
			// set texturing parameters
			//
			gen_mapping_coords is static gen_mapping_coords;
		end;

		if verbose_recycling then
			write "new 3ds spindle", ;
		end;
					
		with translated_3ds_spindle do
		
			// set geometry attributes
			//
			its radius = radius;
			its height = height;
			its cap_height = cap_height;
			its blend = blend;
				
			// set tessellation attributes
			//
			its sides = sides;
			its cap_segments = cap_segments;
			its height_segments = height_segments;
			its smooth is smooth;
				
			// set additional attributes that
			// are not provided by 3DS Max
			//
			its capping is capping;
		end;
	end;    // new  

	//
	// setting method
	//
	
	verb set with
	
		// geometry parameters
		//
		scalar radius = 1;
		scalar height = 2;
		scalar cap_height = .5;
		scalar blend = 0;
			
		// tessellation parameters
		//
		integer sides = 16;
		integer cap_segments = 1;
		integer height_segments = 1;
		boolean smooth is true;
		
		// slicing parameters
		//
		boolean slice_on is false;
		scalar slice_from = 0;
		scalar slice_to = 0;
		
		// texturing parameters
		//
		boolean gen_mapping_coords is false;
			
		// additional parameters that
		// are not provided by 3DS Max
		//
		boolean capping is true;
	is
		with translated_3ds_spindle do
		
			// set geometry attributes
			//
			its radius = radius;
			its height = height;
			its cap_height = cap_height;
			its blend = blend;
				
			// set tessellation attributes
			//
			its sides = sides;
			its cap_segments = cap_segments;
			its height_segments = height_segments;
			its smooth is smooth;
				
			// set additional attributes that
			// are not provided by 3DS Max
			//
			its capping is capping;
			
			// set slicing parameters
			//
			its slice_on is slice_on;
			its slice_from = slice_from;
			its slice_to = slice_to;
					
			// set texturing parameters
			//
			its gen_mapping_coords is gen_mapping_coords;
		end;    
	end;    // set
	
	//
	// collapsing method
	//
	
	translated_3ds_stackable type question get_collapsed is
		translated_3ds_editable_mesh type editable_mesh is translated_3ds_editable_mesh type's get;
		translated_3ds_editable_mesh type part_mesh is none;            
		trans type part_trans;
		translated_3ds_cone type translated_3ds_cone is none;
		translated_3ds_cylinder type translated_3ds_cylinder is none;
		
		translated_3ds_cone is translated_3ds_cone type's get;
		translated_3ds_cone set with                
			radius1 = 0;
			radius2 = radius;
			height = abs cap_height;
			height_segments = cap_segments;
			sides = static sides;
			smooth is static smooth;
			slice_on is static slice_on;
			slice_from = static slice_from;
			slice_to = static slice_to;
			gen_mapping_coords is static gen_mapping_coords;
			capping is static capping;
			capping1 is false;
			capping2 is false;
			
			// create part transformation
			//
			part_trans = unit_trans;
			if static height < 0 then
				move the part_trans to <0 0 static height>;
			end;
		end;
		part_mesh is to_translated_3ds_editable_mesh of translated_3ds_cone;
		translated_3ds_cone recycle;
		part_mesh transform from part_trans;
		editable_mesh add_editable_mesh_elements of part_mesh;
		part_mesh recycle;      
		translated_3ds_cylinder is translated_3ds_cylinder type's get;
		translated_3ds_cylinder set with        
			radius = static radius;
			height = abs static height - abs cap_height * 2;
			height_segments = static height_segments;
			cap_segments = 0;
			sides = static sides;
			smooth is static smooth;
			slice_on is static slice_on;
			slice_from = static slice_from;
			slice_to = static slice_to;
			gen_mapping_coords is static gen_mapping_coords;
			capping is static capping;
			// create part transformation
			//
			part_trans = unit_trans;        
			move the part_trans to <0 0 abs cap_height>;
			if static height < 0 then
				move the part_trans to <0 0 static height>;
			end;
		end;
		part_mesh is to_translated_3ds_editable_mesh of translated_3ds_cylinder;
		translated_3ds_cylinder recycle;
		part_mesh transform from part_trans;
		editable_mesh add_editable_mesh_elements of part_mesh;
		part_mesh recycle;  
		translated_3ds_cone is translated_3ds_cone type's get;
		translated_3ds_cone set with        
			radius1 = radius;
			radius2 = 0;
			height = abs cap_height;
			height_segments = cap_segments;
			sides = static sides;
			smooth is static smooth;
			slice_on is static slice_on;
			slice_from = static slice_from;
			slice_to = static slice_to;
			gen_mapping_coords is static gen_mapping_coords;
			capping1 is false;
			capping2 is false;
			// create part transformation
			//
			part_trans = unit_trans;            
			move the part_trans to <0 0 (abs static height - abs cap_height)>;
			if static height < 0 then
				move the part_trans to <0 0 static height>;
			end;
		end;
		part_mesh is to_translated_3ds_editable_mesh of translated_3ds_cone;
		translated_3ds_cone recycle;
		part_mesh transform from part_trans;
		editable_mesh add_editable_mesh_elements of part_mesh;
		part_mesh recycle;
				
		recycle;
		answer editable_mesh;
	end;    // get_collapsed
		
	//  
	// recycling methods
	//
	
	objective translated_3ds_spindle type question get is
		if some free_list then
		
			// get from free list
			//
			translated_3ds_spindle type next is free_list;
			free_list is its next;
			
			answer next;
		else
			// create new
			//
			answer new translated_3ds_spindle;
		end;
	end;    // get
	
	reference verb recycle is
	
		// add to free list
		//
		next is free_list;
		free_list is translated_3ds_spindle;
		
		translated_3ds_spindle is none;
	end;        // recycle          
end;    // translated_3ds_spindle

//
// procedural interface
//

shape translated_3ds_spindle with
	
	// geometry parameters
	//
	scalar radius = 1;
	scalar height = 2;
	scalar cap_height = .5;
	scalar blend = 0;
		
	// tessellation parameters
	//
	integer sides = 16;
	integer cap_segments = 1;
	integer height_segments = 1;
	boolean smooth is true;
	
	// slicing parameters
	//
	boolean slice_on is false;
	scalar slice_from = 0;
	scalar slice_to = 0;
	
	// texturing parameters
	//
	boolean gen_mapping_coords is false;
		
	// additional parameters that
	// are not provided by 3DS Max
	//
	boolean capping is true;
	
	// modifier stack
	//
	translated_3ds_spindle type translated_3ds_spindle is translated_3ds_spindle type's get;
	modifier_stack push translated_3ds_stackable type translated_3ds_spindle;
is
	// set first entry in modifier stack to object
	//
	translated_3ds_spindle set with
	
		// set geometry parameters
		//
		radius = static radius;
		height = static height;
		cap_height = static cap_height;
		blend = static blend;
			
		// set tessellation parameters
		//
		sides = static sides;
		cap_segments = static cap_segments;
		height_segments = static height_segments;
		smooth is static smooth;
		
		// set slicing parameters
		//
		slice_on is static slice_on;
		slice_from = static slice_from;
		slice_to = static slice_to;
		
		// set texturing parameters
		//
		gen_mapping_coords is static gen_mapping_coords;
			
		// set additional parameters that
		// are not provided by 3DS Max
		//
		capping is static capping;
	end;
	
	// render
	//
	modifier_stack instance;
end;    // translated_3ds_spindle