{***************************************************************}
{ |\  /|                                                We Put  }
{ | >< Hypercosm            3ds_line.ores               3d      }
{ |/  \|                                                To Work }
{***************************************************************}
{                                                               }
{       Contains line definitions that correspond to            }
{       entities from Discreet's 3D Studio Max software.        }
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}

include "system/3ds_shape.ores";
include "system/3ds_editable_spline.ores";
include "system/3ds_modifier_stacks.ores";

subject translated_3ds_line
extends
	translated_3ds_collapsable translated_3ds_shape
does
	// constructor
	//
	verb new
	
		// geometry parameters
		//
		of translated_3ds_splines type splines is none; 
	with
		// shape parameters
		//
		integer steps = 4;
		boolean optimize is false;
		boolean adaptive is false;
		boolean renderable is false;
		scalar thickness = 1;
		
		// texturing parameters
		// 
		boolean gen_mapping_coords is false;
	end;
	
	// setting method
	//
	verb set
	
		// geometry parameters
		//
		to translated_3ds_splines type splines is none; 
	with
		// shape parameters
		//
		integer steps = 4;
		boolean optimize is false;
		boolean adaptive is false;
		boolean renderable is false;
		scalar thickness = 1;
		
		// texturing parameters
		// 
		boolean gen_mapping_coords is false;
	end;
	
	// collapsing method
	//
	translated_3ds_stackable type question get_collapsed;
	
	// recycling methods
	//
	objective translated_3ds_line type question get;
	reference verb recycle; 
has
	// attributes
	//
	translated_3ds_splines type splines;
	
private 
	// recycling attributes
	//
	translated_3ds_line type next is none;  
is
	// recycling free list
	//
	translated_3ds_line type free_list is none;
	
	//
	// constructor
	//
	
	verb new
	
		// geometry parameters
		//
		of translated_3ds_splines type splines is none; 
	with
		// shape parameters
		//
		integer steps = 4;
		boolean optimize is false;
		boolean adaptive is false;
		boolean renderable is false;
		scalar thickness = 1;
		
		// texturing parameters
		// 
		boolean gen_mapping_coords is false;
	is
		translated_3ds_shape new with
		
			// set shape parameters
			//
			steps = static steps;
			optimize is static optimize;
			adaptive is static adaptive;
			renderable is static renderable;
			thickness = static thickness;
			
			// set texturing parameters
			// 
			gen_mapping_coords is static gen_mapping_coords;        
		end;
		
		if verbose_recycling then
			write "new 3ds line", ;
		end;
					
		with translated_3ds_line do
		
			// set geometry attributes
			//
			its splines is splines;
		end;
	end;    // new
	
	//
	// setting method
	//
	
	verb set
	
		// geometry parameters
		//
		to translated_3ds_splines type splines is none; 
	with
		// shape parameters
		//
		integer steps = 4;
		boolean optimize is false;
		boolean adaptive is false;
		boolean renderable is false;
		scalar thickness = 1;
		
		// texturing parameters
		// 
		boolean gen_mapping_coords is false;
	is
		with translated_3ds_line do
		
			// set geometry attributes
			//
			its splines is splines;
			
			// set shape parameters
			//
			its steps = steps;
			its optimize is optimize;
			its adaptive is adaptive;
			its renderable is renderable;
			its thickness = thickness;
			
			// set texturing parameters
			// 
			its gen_mapping_coords is gen_mapping_coords;
		end;    
	end;    // set
		
	//  
	// collapsing method
	//
	
	translated_3ds_stackable type question get_collapsed is
		translated_3ds_editable_spline type editable_spline is none;
		
		// create editable spline
		//
		editable_spline is translated_3ds_editable_spline type's get;
		editable_spline set to splines with
		
			// set shape parameters
			//
			steps = static steps;
			optimize is static optimize;
			adaptive is static adaptive;
			renderable is static renderable;
			thickness = static thickness;
			
			// set texturing parameters
			// 
			gen_mapping_coords is static gen_mapping_coords;
		end;
		splines is none;
			
		recycle;        
		answer editable_spline;
	end;    // get_collapsed
	
		
	//  
	// recycling methods
	//
	
	objective translated_3ds_line type question get is
		if some free_list then
		
			// get from free list
			//
			translated_3ds_line type next is free_list;
			free_list is its next;
			
			answer next;
		else
			// create new
			//
			answer new translated_3ds_line;
		end;
	end;    // get
	
	reference verb recycle is
		// attributes
		//
		for each translated_3ds_spline type spline in splines do
			spline recycle;
		end;
		
		// add to free list
		//
		next is free_list;
		free_list is translated_3ds_line;
		
		translated_3ds_line is none;
	end;        // recycle  
end;    // translated_3ds_line

//
// procedural interface
//

shape translated_3ds_line 
	// geometry parameters
	//
	of translated_3ds_splines type splines; 
with
	// shape parameters
	//
	integer steps = 4;
	boolean optimize is false;
	boolean adaptive is false;
	boolean renderable is false;
	scalar thickness = 1; 
	
	// texturing parameters
	//
	boolean gen_mapping_coords is false;
	
	// modifier stack
	//
	translated_3ds_line type translated_3ds_line is translated_3ds_line type's get;
	modifier_stack push translated_3ds_stackable type translated_3ds_line;
is
	// set first entry in modifier stack to object
	//
	translated_3ds_line set to splines with
		// set shape parameters
		//
		steps = static steps;
		optimize is static optimize;
		adaptive is static adaptive;
		renderable is static renderable;
		thickness = static thickness;
		
		// set texturing parameters
		//
		gen_mapping_coords is static gen_mapping_coords;
	end;
	
	// render
	//
	modifier_stack instance;
end;    // translated_3ds_line