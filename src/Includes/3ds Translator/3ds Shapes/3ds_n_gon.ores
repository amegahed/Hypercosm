{***************************************************************}
{ |\  /|                                                We Put  }
{ | >< Hypercosm           3ds_n_gon.ores               3d      }
{ |/  \|                                                To Work }
{***************************************************************}
{                                                               }
{       Contains n_gon definitions that correspond to           }
{       entities from Discreet's 3D Studio Max software.        }
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************} 

include "system/3ds_shape.ores";
include "system/3ds_editable_spline.ores";
include "system/3ds_modifier_stacks.ores";

subject translated_3ds_n_gon
extends
	translated_3ds_collapsable translated_3ds_shape
does
	// constructor
	//
	verb new with
		// geometry parameters
		//
		scalar radius = 1;
		integer sides = 7;
		boolean circumscribed is false;
			
		// shape parameters
		//
		integer steps = 4;
		boolean optimize is false;
		boolean adaptive is false;
		boolean renderable is false;
		scalar thickness = 1;
		
		// texturing parameters
		//
		boolean gen_mapping_coords is false;
	end;

	// setting method
	//
	verb set with
		
		// geometry parameters
		//
		scalar radius = 1;
		integer sides = 7;
		boolean circumscribed is false;
			
		// shape parameters
		//
		integer steps = 4;
		boolean optimize is false;
		boolean adaptive is false;
		boolean renderable is false;
		scalar thickness = 1;
		
		// texturing parameters
		//
		boolean gen_mapping_coords is false;
	end;
		
	// collapsing method
	//
	translated_3ds_stackable type question get_collapsed;
	
	// recycling methods
	//
	objective translated_3ds_n_gon type question get;
	reference verb recycle; 
has
	// attributes
	//
	scalar radius;
	integer sides;
	boolean circumscribed;
private 
	// recycling attributes
	//
	translated_3ds_n_gon type next is none;
is
	// recycling free list
	//
	translated_3ds_n_gon type free_list is none;
	
	//
	// constructor
	//
	
	verb new with
		// geometry parameters
		//
		scalar radius = 1;
		integer sides = 7;
		boolean circumscribed is false;
		
		// shape parameters
		//
		integer steps = 4;
		boolean optimize is false;
		boolean adaptive is false;
		boolean renderable is false;
		scalar thickness = 1;
		
		// texturing parameters
		//
		boolean gen_mapping_coords is false;
	is
		translated_3ds_shape new with
		
			// set shape parameters
			//
			steps = static steps;
			optimize is static optimize;
			adaptive is static adaptive;
			renderable is static renderable;
			thickness = static thickness;
			
			// set texturing parameters
			// 
			gen_mapping_coords is static gen_mapping_coords;
		end;
		
		if verbose_recycling then
			write "new 3ds n gon", ;
		end;
					
		with translated_3ds_n_gon do
		
			// set geometry attributes
			//
			its radius = radius;
			its sides = sides;
			its circumscribed is circumscribed;
		end;
	end;    // new
	
	//
	// setting method
	//
	
	verb set with
		
		// geometry parameters
		//
		scalar radius = 1;
		integer sides = 7;
		boolean circumscribed is false;
			
		// shape parameters
		//
		integer steps = 4;
		boolean optimize is false;
		boolean adaptive is false;
		boolean renderable is false;
		scalar thickness = 1;
		
		// texturing parameters
		//
		boolean gen_mapping_coords is false;
	is
		with translated_3ds_n_gon do
		
			// set geometry attributes
			//
			its radius = radius;
			its sides = sides;
			its circumscribed is circumscribed;
			
			// set shape parameters
			//
			its steps = steps;
			its optimize is optimize;
			its adaptive is adaptive;
			its renderable is renderable;
			its thickness = thickness;
			
			// set texturing parameters
			// 
			its gen_mapping_coords is gen_mapping_coords;
		end;    
	end;    // set
		
	//  
	// collapsing method
	//
	
	translated_3ds_stackable type question get_collapsed is
		translated_3ds_spline_vertices type vertices;
		translated_3ds_spline_segments type segments;
		translated_3ds_spline type spline is none;
		translated_3ds_splines type splines is none;
		translated_3ds_editable_spline type editable_spline is none;
		
		// geometry variables
		//
		scalar r = radius;
		
		if circumscribed then
			r = itself / cos (360 / (sides * 2));
		end;
		
		// create vertices
		//
		dim none vertices[1 .. sides];
		for integer counter = 1 .. sides do
			scalar t = counter / sides;
			scalar u = t * 360;
			scalar x = cos u * r;
			scalar y = sin u * r;
			vertices[counter] is translated_3ds_corner_vertex at <x y 0>;
		end;
		
		// create segments
		//
		dim none segments[1 .. sides];
		for integer counter = 1 .. sides - 1 do
			segments[counter] is translated_3ds_spline_segment type's get;
			segments[counter] set line from vertices[counter] to vertices[counter + 1];
		end;
		segments[sides] is translated_3ds_spline_segment type's get;
		segments[sides] set line from vertices[sides] to vertices[1];
		
		// create spline
		//
		spline is translated_3ds_spline type's get;
		spline append_vertices vertices;
		spline append_segments segments;
		
		// create splines
		//
		splines is [spline];
				
		// create editable spline
		//
		editable_spline is translated_3ds_editable_spline type's get;
		editable_spline set to splines with
		
			// set shape parameters
			//
			steps = static steps;
			optimize is static optimize;
			adaptive is static adaptive;
			renderable is static renderable;
			thickness = static thickness;
			
			// set texturing parameters
			// 
			gen_mapping_coords is static gen_mapping_coords;
		end;
						
		recycle;
		answer editable_spline;
	end;    // get_collapsed
	
	//  
	// recycling methods
	//
	
	objective translated_3ds_n_gon type question get is
		if some free_list then
		
			// get from free list
			//
			translated_3ds_n_gon type next is free_list;
			free_list is its next;
			
			answer next;
		else
			// create new
			//
			answer new translated_3ds_n_gon;
		end;
	end;    // get
	
	
	reference verb recycle is
	
		// add to free list
		//
		next is free_list;
		free_list is translated_3ds_n_gon;
		
		translated_3ds_n_gon is none;
	end;        // recycle  
end;    // translated_3ds_n_gon

//
// procedural interface
//

shape translated_3ds_n_gon with

	// geometry parameters
	//
	scalar radius = 1;
	integer sides = 7;
	boolean circumscribed is false;
	
	// shape parameters
	//
	integer steps = 4;
	boolean optimize is false;
	boolean adaptive is false;
	boolean renderable is false;
	scalar thickness = 1;
	
	// texturing parameters
	// 
	boolean gen_mapping_coords is false;

	// modifier stack
	//
	translated_3ds_n_gon type translated_3ds_n_gon is translated_3ds_n_gon type's get;
	modifier_stack push translated_3ds_stackable type translated_3ds_n_gon;
is
	// set first entry in modifier stack to object
	//
	translated_3ds_n_gon set with
		// set geometry parameters
		//
		radius = static radius;
		sides = static sides;
		circumscribed is static circumscribed; 
			
		// set shape parameters
		//
		steps = static steps;
		optimize is static optimize;
		adaptive is static adaptive;
		renderable is static renderable;
		thickness = static thickness;
		
		// set texturing parameters
		//
		gen_mapping_coords is static gen_mapping_coords;
	end;
	
	// render
	//
	modifier_stack instance;
end;    // translated_3ds_n_gon