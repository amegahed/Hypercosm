{***************************************************************}
{ |\  /|                                                We Put  }
{ | >< Hypercosm    3ds_bezier_float_controller.ores    3d      }
{ |/  \|                                                To Work }
{***************************************************************}
{                                                               }
{       Contains controller definitions that correspond to      }
{       entities from Discreet's 3D Studio Max software.        }
{                                                               }
{***************************************************************}
{                Copyright (c) 2004 Hypercosm, LLC.             }
{***************************************************************}

include "system/3ds_keys.ores";
include "system/3ds_bezier_float_key.ores";
include "system/3ds_key_controller.ores";
include "system/3ds_float_controlling.ores";
include "system/hermite_splines.ores";

subject translated_3ds_bezier_float_controller 
extends
	translated_3ds_float_controllable translated_3ds_key_controller
does
	// constructor
	//
	verb new
		of scalar values[];
		from scalar in_tangents[];
		to scalar out_tangents[];
		at translated_3ds_time type times[];
	end;
	
	// getting method
	//
	scalar question get_value
		at translated_3ds_time type t;
	end;
is
	//
	// objective querying methods
	//

	objective translated_3ds_keys type question bezier_float_keys
		of scalar values[];
		from scalar in_tangents[];
		to scalar out_tangents[];
		at translated_3ds_time type times[];
	is
		translated_3ds_keys type keys;

		// create array of keys
		//
		if some values then
			integer key_number = num values;
			dim none keys[1 .. key_number];
			for integer counter = 1 .. key_number do
				keys[counter] is new translated_3ds_bezier_float_key
					of values[counter] from in_tangents[counter] to out_tangents[counter]
					at times[counter];
			end;
		end;
		
		answer keys;
	end;    // bezier_float_keys
	
	//
	// constructor
	//
	
	verb new
		of scalar values[];
		from scalar in_tangents[];
		to scalar out_tangents[];
		at translated_3ds_time type times[];
	is
		translated_3ds_key_controller new using bezier_float_keys 
			of values from in_tangents to out_tangents at times;
	end;    // new
	
	//
	// getting method
	//
	
	scalar question get_value
		at translated_3ds_time type time;
	is
		translated_3ds_bezier_float_key type prev_key is
			translated_3ds_bezier_float_key type (keys get_prev at time);
		translated_3ds_bezier_float_key type next_key is
			translated_3ds_bezier_float_key type (keys get_next at time);
		
		if some prev_key and some next_key then
			polynomial type polynomial is get_hermite_cubic
				from prev_key's value prev_key's out_tangent
				to next_key's value (-next_key's in_tangent);
			scalar duration = next_key's time - prev_key's time;
			scalar t = (time - prev_key's time) / duration;
			answer polynomial get_value at t;
		elseif some next_key then
			answer next_key's value;
		elseif some prev_key then
			answer prev_key's value;
		else
			answer 0;
		end;
	end;    // get_value
end;    // translated_3ds_bezier_float_controller